<?xml version="1.0" encoding="utf-8"?>
<!--
Copyright (C) 2013  hippoandfriends

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/gpl.txt>.

-->
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" title="DownloadFoodTableView"
		initialize="onInitialize()"
		backKeyPressed="view1_backKeyPressedHandler(event)">
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Metadata>
		[ResourceBundle("downloadfoodtableview")]
	</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayList;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import spark.events.IndexChangeEvent;
			import spark.events.PopUpEvent;
			
			import myComponents.AlertPopUp;
			
			import utilities.Synchronize;
			
			[Bindable]
			private var itemList:ArrayList;
			[Bindable]
			private var back_label:String;
			
			private var alertPopUp:AlertPopUp;
			
			private var _selectedSpreadSheetIndex:int;
			
			//private static var popToFirstViewNecessary:Boolean = false;
			
			private static var downloadOptions:ArrayList;
			
			private var _selectedWorkSheetIndex:int;
			
			private function onInitialize():void {
				//Synchronize.getInstance().addEventListener(Synchronize.SYNCHRONIZE_ERROR_OCCURRED,onSynchronizeErrorOccurred);
				
				if (downloadOptions == null) {
					downloadOptions = new ArrayList();
					downloadOptions.addItem(resourceManager.getString('downloadfoodtableview','overwrite'));
					downloadOptions.addItem(resourceManager.getString('downloadfoodtableview','append'));
				}
				back_label = resourceManager.getString('general','back');
				title = resourceManager.getString('downloadfoodtableview','title');
				topText.text = resourceManager.getString('downloadfoodtableview','searchingfortables');
				Synchronize.getInstance().addEventListener(Synchronize.SPREADSHEET_LIST_RETRIEVED,onSpreadSheetListRetrieved);
				Synchronize.getInstance().googleExcelFindAllSpreadSheets();			
				spreadSheetList.addEventListener(Event.CHANGE,selectedSpreadSheet);
			}
			
			private function onSynchronizeErrorOccurred(event:Event):void {
				alertPopUp = new AlertPopUp();
				alertPopUp.addEventListener(PopUpEvent.CLOSE, okClicked);
				alertPopUp.show(this);
				alertPopUp.setMessage(Synchronize.getInstance().synchronize_debugString);
			}
			
			private function okClicked(event: PopUpEvent):void {
				if (alertPopUp != null) {
					PopUpManager.removePopUp(alertPopUp);
					if (alertPopUp.hasEventListener(PopUpEvent.CLOSE)) {
						alertPopUp.removeEventListener(PopUpEvent.CLOSE, okClicked);
					}
				}
			}
			
			private function onSpreadSheetListRetrieved(event:Event):void {
				itemList = Synchronize.getInstance().spreadSheetList;
				Synchronize.getInstance().removeEventListener(Synchronize.SPREADSHEET_LIST_RETRIEVED,onSpreadSheetListRetrieved);
				
				if (itemList.length == 0) {
					topText.text = resourceManager.getString('downloadfoodtableview','nospreadsheetsfound');
				}
				else  {
					topText.text = resourceManager.getString('downloadfoodtableview','selectspreadsheetsfound');
				}
			}
			
			protected function view1_backKeyPressedHandler(event:FlexEvent):void {
				Synchronize.getInstance().removeEventListener(Synchronize.SPREADSHEET_LIST_RETRIEVED,onSpreadSheetListRetrieved);
			}
			
			private function selectedSpreadSheet(event:IndexChangeEvent):void {
				//(event.currentTarget as List).selectedIndex
				spreadSheetList.visible = false;
				_selectedSpreadSheetIndex = (event.currentTarget as List).selectedIndex;
				topText.text = resourceManager.getString('downloadfoodtableview','searchinglistofworksheets') + " " + itemList.getItemAt(_selectedSpreadSheetIndex).title;
				Synchronize.getInstance().addEventListener(Synchronize.WORKSHEETS_IN_FOODTABLE_RETRIEVED,onWorkSheetListRetrieved);
				
				Synchronize.getInstance().googleExcelFindAllWorkSheetsInFoodTableSpreadSheet(null,_selectedSpreadSheetIndex);
			}
			
			private function onWorkSheetListRetrieved(event:Event):void {
				
				spreadSheetList.removeEventListener(Event.CHANGE,selectedSpreadSheet);
				itemList = Synchronize.getInstance().workSheetList;
				if (itemList.length == 1)  {
					selectedWorkSheet(null);
					return;
				}
				topText.text = resourceManager.getString('downloadfoodtableview','spreadsheet') +
					" " +
					displaySpreadSheetTittle(Synchronize.getInstance().spreadSheetList.getItemAt(_selectedSpreadSheetIndex)) +
					" " +
					resourceManager.getString('downloadfoodtableview','has') + 
					" " +
					itemList.length +
					" " +
					resourceManager.getString('downloadfoodtableview','worksheets') + 
					"\n\n" + 
					resourceManager.getString('downloadfoodtableview','selectworksheetsfound');
				spreadSheetList.addEventListener(Event.CHANGE,selectedWorkSheet);
				Synchronize.getInstance().removeEventListener(Synchronize.WORKSHEETS_IN_FOODTABLE_RETRIEVED,onSpreadSheetListRetrieved);
				spreadSheetList.visible = true;
			}
			
			private function displaySpreadSheetTittle(item:Object):String {
				if (item is String)
					return item as String;
				if (!(item is XML)) 
					return item.title;
				
				var namespace:Namespace = Synchronize.getInstance().namespace_default;
				var temp:String = (item as XML)..namespace::title;
				return temp;
			}
			
			private function selectedWorkSheet(event:IndexChangeEvent = null):void {
				spreadSheetList.removeEventListener(Event.CHANGE,selectedWorkSheet);
				spreadSheetList.visible = false;
				if (event == null)
					_selectedWorkSheetIndex = 0;
				else
					_selectedWorkSheetIndex = event.newIndex;
				
				topText.text = resourceManager.getString('downloadfoodtableview','overwriteorappend') ;
				itemList = downloadOptions;
				spreadSheetList.visible = true
				spreadSheetList.addEventListener(Event.CHANGE,selectedDownLoadOption);
			}
			
			private function selectedDownLoadOption(event:IndexChangeEvent):void {
				var dataObject:Object = new Object();
				dataObject.selectedWorkSheetIndex = _selectedWorkSheetIndex;
				dataObject.selectedSpreadSheetIndex = _selectedSpreadSheetIndex;
				dataObject.overWriteDatabase = (event.newIndex == 0);
				navigator.pushView(DownloadFoodTableView2,dataObject);
			}
			
		]]>
	</fx:Script>
	<s:navigationContent>
		<s:Button   label="{back_label}" id="backButton" click="navigator.popView()" height="48"/>
	</s:navigationContent>
	<s:Label id="topText" width="100%" top="5"/>
	<s:Line width="100%" left="0" right="0" top="{topText.height + 7}">
		<s:stroke>
			<s:SolidColorStroke color="0x000000" weight="1"/>
		</s:stroke>
	</s:Line>	
	
	<s:List id = "spreadSheetList" left="0" right="0" top="{topText.height + 25}" bottom="0" width="100%" useVirtualLayout="true" 
			itemRenderer="myComponents.ListItemRenderer" labelFunction="displaySpreadSheetTittle"
			dataProvider="{itemList}"/>
	
</s:View>
