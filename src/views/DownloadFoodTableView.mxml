<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" title="DownloadFoodTableView"
		initialize="onInitialize()"
		backKeyPressed="view1_backKeyPressedHandler(event)">
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Metadata>
		[ResourceBundle("downloadfoodtableview")]
	</fx:Metadata>

	<fx:Script>
		<![CDATA[
			import databaseclasses.Database;
			
			import mx.collections.ArrayList;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import myComponents.AlertPopUp;
			
			import spark.components.TabbedViewNavigator;
			import spark.events.IndexChangeEvent;
			import spark.events.PopUpEvent;
			
			import utilities.Synchronize;
			
			[Bindable]
			private var itemList:ArrayList;
			[Bindable]
			private var back_label:String;

			private var alertPopUp:AlertPopUp;
		
			private var _selectedSpreadSheetIndex:int;
			
			private static var popToFirstViewNecessary:Boolean = false;

			public function get selectedSpreadSheetIndex():int
			{
				return _selectedSpreadSheetIndex;
			}

			private var _selectedWorkSheetIndex:int;

			public function get selectedWorkSheetIndex():int
			{
				return _selectedWorkSheetIndex;
			}
			
			private function onInitialize():void {
				if (popToFirstViewNecessary) {
					navigator.popToFirstView();
					popToFirstViewNecessary = false;
				}
				back_label = resourceManager.getString('general','back');
				title = resourceManager.getString('downloadfoodtableview','title');
				topText.text = resourceManager.getString('downloadfoodtableview','searchingfortables');
				Synchronize.getInstance().addEventListener(Synchronize.SPREADSHEET_LIST_RETRIEVED,onSpreadSheetListRetrieved);
				Synchronize.getInstance().googleExcelFindAllSpreadSheets();			
				spreadSheetList.addEventListener(Event.CHANGE,selectedSpreadSheet);
			}
			
			private function onSpreadSheetListRetrieved(event:Event):void {
				itemList = Synchronize.getInstance().spreadSheetList;
				Synchronize.getInstance().removeEventListener(Synchronize.SPREADSHEET_LIST_RETRIEVED,onSpreadSheetListRetrieved);
				if (itemList.length == 0)
					topText.text = resourceManager.getString('downloadfoodtableview','nospreadsheetsfound');
				else  {
					topText.text = resourceManager.getString('downloadfoodtableview','selectspreadsheetsfound');
				}
			}
			
			protected function view1_backKeyPressedHandler(event:FlexEvent):void {
				Synchronize.getInstance().removeEventListener(Synchronize.SPREADSHEET_LIST_RETRIEVED,onSpreadSheetListRetrieved);
			}
			
			private function selectedSpreadSheet(event:IndexChangeEvent):void {
				//(event.currentTarget as List).selectedIndex
				spreadSheetList.visible = false;
				_selectedSpreadSheetIndex = (event.currentTarget as List).selectedIndex;
				topText.text = resourceManager.getString('downloadfoodtableview','searchinglistofworksheets') + " " + itemList.getItemAt(_selectedSpreadSheetIndex).title;
				Synchronize.getInstance().addEventListener(Synchronize.WORKSHEETS_IN_FOODTABLE_RETRIEVED,onWorkSheetListRetrieved);

				Synchronize.getInstance().googleExcelFindAllWorkSheetsInFoodTableSpreadSheet(null,_selectedSpreadSheetIndex);
			}
			
			private function onWorkSheetListRetrieved(event:Event):void {
				
				spreadSheetList.removeEventListener(Event.CHANGE,selectedSpreadSheet);
				itemList = Synchronize.getInstance().workSheetList;
				if (itemList.length == 1)  {
					selectedWorkSheet(null);
					return;
				}
				topText.text = resourceManager.getString('downloadfoodtableview','spreadsheet') +
					" " +
					displaySpreadSheetTittle(Synchronize.getInstance().spreadSheetList.getItemAt(_selectedSpreadSheetIndex)) +
					" " +
					resourceManager.getString('downloadfoodtableview','has') + 
					" " +
					itemList.length +
					" " +
					resourceManager.getString('downloadfoodtableview','worksheets') + 
					"\n\n" + 
					resourceManager.getString('downloadfoodtableview','selectworksheetsfound');
				spreadSheetList.addEventListener(Event.CHANGE,selectedWorkSheet);
				Synchronize.getInstance().removeEventListener(Synchronize.WORKSHEETS_IN_FOODTABLE_RETRIEVED,onSpreadSheetListRetrieved);
				spreadSheetList.visible = true;
			}
			
			private function displaySpreadSheetTittle(item:Object):String {
				if (!(item is XML))
					return item.title;
				
				var namespace:Namespace = Synchronize.getInstance().namespace_default;
				var temp:String = (item as XML)..namespace::title;
				return temp;
			}
			
			private function selectedWorkSheet(event:IndexChangeEvent = null):void {
				spreadSheetList.visible = false;
				if (event == null)
					_selectedWorkSheetIndex = 0;
				else
					_selectedWorkSheetIndex = event.newIndex;
				
				topText.text = resourceManager.getString('downloadfoodtableview','importingspreadsheet') + 
					" " + 
					displaySpreadSheetTittle(Synchronize.getInstance().spreadSheetList.getItemAt(_selectedSpreadSheetIndex)) +
					", " +
					resourceManager.getString('downloadfoodtableview','worksheet') +
					" " +
					displaySpreadSheetTittle(Synchronize.getInstance().workSheetList.getItemAt(_selectedWorkSheetIndex));
				
				Synchronize.getInstance().addEventListener(Synchronize.FOODTABLE_DOWNLOADED,onFoodTableDownloaded);
				Synchronize.getInstance().googleExcelDownloadFoodTableSpreadSheet(null,_selectedSpreadSheetIndex,_selectedWorkSheetIndex);
			}
			
			private function onFoodTableDownloaded(event:Event):void {
				Synchronize.getInstance().removeEventListener(Synchronize.FOODTABLE_DOWNLOADED,onFoodTableDownloaded);	
				
				topText.text = resourceManager.getString('downloadfoodtableview','storingnewfoodtable');

				Database.getInstance().addEventListener(Database.NEW_FOOD_DATABASE_STORED_SUCCESS,onFoodTableStoredInDatabase);
				Database.getInstance().addEventListener(Database.NEW_FOOD_DATABASE_STORED_FAILED,onFoodTableStoredInDatabase);
				Database.getInstance().addEventListener(Database.NEW_FOOD_DATABASE_STATUS_UPDATE,onFoodTableStatusUpdate);

				Database.getInstance().loadFoodTable(Synchronize.getInstance().foodtable);
			}
			
			private function onFoodTableStatusUpdate(event:Event):void {
				topText.text = resourceManager.getString('downloadfoodtableview','storingnewfoodtable') +
					"\n"+
					Database.getInstance().
						newFoodDatabaseStatus.
							replace("{outof}",resourceManager.getString('downloadfoodtableview','outof')).
							replace("{elementsloaded}",resourceManager.getString('downloadfoodtableview','elementsloaded'));					                          
											
			}
			
			private function onFoodTableStoredInDatabase(event:Event):void {
				Database.getInstance().removeEventListener(Database.NEW_FOOD_DATABASE_STORED_SUCCESS,onFoodTableStoredInDatabase);
				Database.getInstance().removeEventListener(Database.NEW_FOOD_DATABASE_STORED_FAILED,onFoodTableStoredInDatabase);
				alertPopUp = new AlertPopUp();
				alertPopUp.addEventListener(PopUpEvent.CLOSE, okClicked);
				if (event.type == Database.NEW_FOOD_DATABASE_STORED_SUCCESS) 
					alertPopUp.show(this,resourceManager.getString('downloadfoodtableview','newfoodtablestoredsuccess'));
				else
					alertPopUp.show(this,resourceManager.getString('downloadfoodtableview','newfoodtablestoredfailed'));

				function okClicked(event: PopUpEvent):void {
					if (alertPopUp != null) {
						PopUpManager.removePopUp(alertPopUp);
						if (alertPopUp.hasEventListener(PopUpEvent.CLOSE)) {
							alertPopUp.removeEventListener(PopUpEvent.CLOSE, okClicked);
						}
						if (navigator)
							navigator.popView();
						popToFirstViewNecessary = true;
					}
				}
			}
		]]>
	</fx:Script>
	<s:navigationContent>
		<s:Button   label="{back_label}" id="backButton" click="navigator.popView()" height="48"/>
	</s:navigationContent>
	<s:Group y="0" x="0" width="100%" left="5" top="5" right="5">
		<s:layout>
			<s:VerticalLayout>
			</s:VerticalLayout>
		</s:layout>
		<s:Label id="topText" width="100%"/>
		<s:List id = "spreadSheetList" left="0" right="0" top="0" bottom="0" width="100%" useVirtualLayout="true" 
				itemRenderer="myComponents.ListItemRenderer" labelFunction="displaySpreadSheetTittle"
				dataProvider="{itemList}"/>
	</s:Group>
	
</s:View>
