<?xml version="1.0" encoding="utf-8"?>
<!--
Copyright (C) 2016  hippoandfriends

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/gpl.txt>.

-->
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" title=""
		initialize="onInitialize()"
		add="addHandler()"
		creationComplete="onCreationComplete()" actionBarVisible="true"
		backKeyPressed="onBackKeyPressed(event)">
	<fx:Declarations>
		<s:DateTimeFormatter id="dateformatter" dateTimePattern="{resourceManager.getString('general','datepattern')}" useUTC="false" locale="{Capabilities.language.substr(0,2)}"/>
		<s:DateTimeFormatter id ="timeformatter" dateTimePattern="{resourceManager.getString('general','timepattern')}" useUTC="false"/>
	</fx:Declarations>
	<fx:Metadata>
		[ResourceBundle("editexerciseeventview")]
	</fx:Metadata>
	<fx:Script>
		<![CDATA[
			import com.distriqt.extension.dialog.DateTimeDialogView;
			import com.distriqt.extension.dialog.Dialog;
			import com.distriqt.extension.dialog.DialogType;
			import com.distriqt.extension.dialog.builders.DateTimeDialogBuilder;
			import com.distriqt.extension.dialog.events.DialogDateTimeEvent;
			import com.distriqt.extension.dialog.events.DialogViewEvent;
			
			import mx.collections.ArrayCollection;
			import mx.core.UIComponent;
			import mx.managers.PopUpManager;
			
			import spark.components.TabbedViewNavigator;
			import spark.components.ViewNavigator;
			
			import databaseclasses.BloodGlucoseEvent;
			import databaseclasses.ExerciseEvent;
			import databaseclasses.Settings;
			import databaseclasses.UnitChangedEvent;
			
			import model.ModelLocator;
			
			import myComponents.DatePicker;
			import myComponents.DayLineWithTotalAmount;
			import myComponents.HelpCallOut;
			import myComponents.PickerEvent;
			import myComponents.RequestConfirmation;
			import myComponents.TimePicker;
			import myComponents.TrackingViewElement;
			import myComponents.elementFromListPicker;
			
			import utilities.DateTimeUtilities;
			import utilities.MyGATracker;
			import utilities.Synchronize;
			
			private var exercisePicker:elementFromListPicker;
			private var dateOrTimePicker:Group;
			
			[Bindable]
			private var exerciseName:String;
			[Bindable]
			private var dateText:String ;
			[Bindable]
			private var timeText:String;
			[Bindable]
			private var back_label:String;

			private var helpCallOut:HelpCallOut;

			private var comment:String;
			
			private var theExerciseEvent:ExerciseEvent;
			/**
			 * if true then this view is created for a new mealevent 
			 */
			private var newExerciseEvent:Boolean;
			
			private var tempDate:int = 0;
			private var tempMonth:int = 0;
			private var tempYear:int = 0;
			private var tempHour:int = 0;
			private var tempMinute:int = 0;
			private var tempSeconds:int = 0;

			private var confirmationPicker:RequestConfirmation;
			
			private var _eventDateAndTime:Date;
			private function get eventDateAndTime():Date
			{
				return _eventDateAndTime;
			}
			private function set eventDateAndTime(value:Date):void
			{
				_eventDateAndTime = value;
				dateText = dateformatter.format(_eventDateAndTime);
				timeText = timeformatter.format(_eventDateAndTime);
			}
			
			private function onCreationComplete():void {
				if (data is ExerciseEvent) {
					if (TrackingView.popToFirstViewNecessary())
						navigator.popToFirstView();
					else {
						theExerciseEvent = data as ExerciseEvent;
						eventDateAndTime = new Date(theExerciseEvent.timeStamp);
						dateText = dateformatter.format(_eventDateAndTime);
						timeText = timeformatter.format(_eventDateAndTime);
						exerciseName = theExerciseEvent.level;
						comment = theExerciseEvent.comment;
						newExerciseEvent = false;
						backButton.visible=true;
						TrashButton.visible = true;
					}
				} else {
					if (Settings.getInstance().getSetting(Settings.SettingsHelpTextEditExerciseEventViewOkButton) == 'true') {
						helpCallOut = new HelpCallOut();
						helpCallOut.helpText = resourceManager.getString('editexerciseeventview','help_text_OkButton');
						helpCallOut.settingId = Settings.SettingsHelpTextEditExerciseEventViewOkButton;
						helpCallOut.addEventListener(PickerEvent.PICKER_SET,helptext0Removed);
						helpCallOut.open(OkButton,true);
					} else {
						helptext0Removed(null);
					}
				}
			}

			private function helptext0Removed(event:Event):void {
				if (helpCallOut != null) helpCallOut.removeEventListener(PickerEvent.PICKER_SET,helptext0Removed);
				if (Settings.getInstance().getSetting(Settings.SettingsHelpTextEditExerciseEventViewChangeExerciseType) == 'true') {
					helpCallOut = new HelpCallOut();
					helpCallOut.helpText = resourceManager.getString('editexerciseeventview','help_text_selectExerciseType');
					helpCallOut.settingId = Settings.SettingsHelpTextEditExerciseEventViewChangeExerciseType;
					helpCallOut.addEventListener(PickerEvent.PICKER_SET,helptext1Removed);
					helpCallOut.open(exerciseNameTextField,true);
				} else {
					helptext1Removed(null);
				}
			}
			
			private function helptext1Removed(event:Event):void {
				if (helpCallOut != null) helpCallOut.removeEventListener(PickerEvent.PICKER_SET,helptext1Removed);
				if (Settings.getInstance().getSetting(Settings.SettingsHelpTextEditBGEventViewChangeDateAndTime) == 'true') {
					helpCallOut = new HelpCallOut();
					helpCallOut.helpText = resourceManager.getString('editbgeventview','help_text_changeDateAndTime');
					helpCallOut.settingId = Settings.SettingsHelpTextEditBGEventViewChangeDateAndTime;
					helpCallOut.addEventListener(PickerEvent.PICKER_SET,helptext2Removed);
					helpCallOut.open(timeGroup,true)
				} else {
					helptext2Removed(null);
				}
			}
			
			private function helptext2Removed(event:Event):void {
				if (helpCallOut != null) helpCallOut.removeEventListener(PickerEvent.PICKER_SET,helptext2Removed);
			}
			
			private function onBackKeyPressed(event:Event):void {
				if (exercisePicker != null) {
					if (exercisePicker.isPopUp) {
						PopUpManager.removePopUp(exercisePicker);
						event.preventDefault();
					}
					exercisePicker = null;
				} 
				if (dateOrTimePicker != null) {
					if (dateOrTimePicker.isPopUp) {
						PopUpManager.removePopUp(dateOrTimePicker);
						event.preventDefault();
					}
					dateOrTimePicker = null;
				} 
				if (confirmationPicker != null) {
					if (confirmationPicker.isPopUp) {
						PopUpManager.removePopUp(confirmationPicker);
						event.preventDefault();
					}
				}
			}
			
			private function onInitialize():void {
				//setting initial values, will be overwritten in oncreationcomplete if data is an medicinevent
				eventDateAndTime = new Date();
				dateText = dateformatter.format(_eventDateAndTime);
				timeText = timeformatter.format(_eventDateAndTime);
				exerciseName = Settings.getInstance().getSetting(parseInt(Settings.getInstance().getSetting(Settings.SettingsDefaultExercise)));
				newExerciseEvent = true;
				back_label = resourceManager.getString('general','back');
				TrashButton.visible = false;
			}
			
			private function exerciseGroupClicked(e:MouseEvent = null):void {
				var unitList:ArrayCollection = new ArrayCollection();
				for (var i:int = Settings.SettingsExerciseType1; i <= Settings.SettingsExerciseType5;i++) {
					if (Settings.getInstance().getSetting(i) != null)
						if (Settings.getInstance().getSetting(i) != "")
							unitList.addItem(Settings.getInstance().getSetting(i));
				}
				
				exercisePicker = new elementFromListPicker();
				exercisePicker.addEventListener(UnitChangedEvent.ITEM_SELECTED, exerciseSelected);
				exercisePicker.elements = unitList;
				exercisePicker.labelText1 = resourceManager.getString('editexerciseeventview','select_intensity');
				
				PopUpManager.addPopUp(exercisePicker,UIComponent(this.parentApplication),true);
				PopUpManager.centerPopUp(exercisePicker);
				
				function exerciseSelected(event:UnitChangedEvent):void {
					exercisePicker.removeEventListener(UnitChangedEvent.ITEM_SELECTED,exerciseSelected);
					PopUpManager.removePopUp(exercisePicker);
					exerciseName = Settings.getInstance().getSetting(Settings.SettingsExerciseType1 + event.index);
				}
			}
			
			/*private function dateClicked(e:MouseEvent = null):void {
				dateOrTimePicker  = new DatePicker();
				(dateOrTimePicker as DatePicker).currentDate = eventDateAndTime;
				dateOrTimePicker.addEventListener(PickerEvent.PICKER_SET, onTimeOrDateSet);
				dateOrTimePicker.addEventListener(PickerEvent.PICKER_CANCEL, onTimeOrDateCancel);
				PopUpManager.addPopUp(dateOrTimePicker, this, true);
				PopUpManager.centerPopUp(dateOrTimePicker);
			}*/
			
			/*private function onTimeOrDateCancel(event: PickerEvent): void
			{
				dateOrTimePicker.removeEventListener(PickerEvent.PICKER_SET, onTimeOrDateSet);
				dateOrTimePicker.removeEventListener(PickerEvent.PICKER_CANCEL, onTimeOrDateCancel);
			}*/
			
			/*private function onTimeOrDateSet(event: PickerEvent): void
			{
				dateOrTimePicker.removeEventListener(PickerEvent.PICKER_SET, onTimeOrDateSet);
				dateOrTimePicker.removeEventListener(PickerEvent.PICKER_CANCEL, onTimeOrDateCancel);
				eventDateAndTime = (event.newValue as Date);
			}*/
			
			/*private function timeClicked(e:MouseEvent = null):void {
				dateOrTimePicker = new TimePicker();
				(dateOrTimePicker as TimePicker).currentTime = eventDateAndTime;
				dateOrTimePicker.addEventListener(PickerEvent.PICKER_SET, onTimeOrDateSet);
				dateOrTimePicker.addEventListener(PickerEvent.PICKER_CANCEL, onTimeOrDateCancel);
				PopUpManager.addPopUp(dateOrTimePicker, this, true);
				PopUpManager.centerPopUp(dateOrTimePicker);
			}*/
			
			private function okClicked(e:MouseEvent = null):void {
				MyGATracker.getInstance().trackPageview( "EditExerciseEventView-OkClicked" );

				if (newExerciseEvent) {
					theExerciseEvent = new ExerciseEvent(
						exerciseName,
						comment,
						DateTimeUtilities.createEventId() + "-exercise",
						eventDateAndTime.valueOf());
					ModelLocator.trackingList.addItem(theExerciseEvent);
				} else {
					theExerciseEvent.updateExerciseEvent(exerciseName,eventDateAndTime.valueOf(),new Date().valueOf(),comment);
				}
				ModelLocator.checkYoungestAndOldestDayLine(new Date(theExerciseEvent.timeStamp));
				ModelLocator.trackingList.refresh();
				//switch to the tracking view and set trackingeventtoshow to this event
				ModelLocator.trackingEventToShow = theExerciseEvent.eventid;
				(navigator.parentNavigator as TabbedViewNavigator).selectedIndex = 1;
				((navigator.parentNavigator as TabbedViewNavigator).navigators[1] as ViewNavigator).popToFirstView();
				Synchronize.getInstance().startSynchronize(true,false);
			}
			private function deleteEvent(e:MouseEvent = null):void {
				confirmationPicker = new RequestConfirmation();
				confirmationPicker.labelText1 = resourceManager.getString('general','delete_event');
				confirmationPicker.addEventListener(PickerEvent.PICKER_SET, onYesClicked);
				confirmationPicker.addEventListener(PickerEvent.PICKER_CANCEL, onNoClicked);
				
				PopUpManager.addPopUp(confirmationPicker, this, true);
				PopUpManager.centerPopUp(confirmationPicker);
				
				function onYesClicked(event: PickerEvent): void
				{
					MyGATracker.getInstance().trackPageview( "EditExerciseEventView-deleteClicked" );

					confirmationPicker.removeEventListener(PickerEvent.PICKER_SET, onYesClicked);
					confirmationPicker.removeEventListener(PickerEvent.PICKER_CANCEL, onNoClicked);
					Synchronize.getInstance().addObjectToBeDeleted(theExerciseEvent);
					Synchronize.getInstance().startSynchronize(true,false);

					var trackinglistPointer:Number = ModelLocator.trackingList.getItemIndex(theExerciseEvent);
					theExerciseEvent.deleteEvent(trackinglistPointer);

					//and now make sure the previous item is visible
					if (trackinglistPointer > 0)
						ModelLocator.trackingEventToShow = 
							(ModelLocator.trackingList.getItemAt(trackinglistPointer - 1) as TrackingViewElement).eventid;						
					//switch to the tracking view
					(navigator.parentNavigator as TabbedViewNavigator).selectedIndex = 1;
					((navigator.parentNavigator as TabbedViewNavigator).navigators[1] as ViewNavigator).popToFirstView();
				}
				
				function onNoClicked(event: PickerEvent): void
				{
					confirmationPicker.removeEventListener(PickerEvent.PICKER_SET, onYesClicked);
					confirmationPicker.removeEventListener(PickerEvent.PICKER_CANCEL, onNoClicked);
				}
			}
			private function editNote(e:MouseEvent = null):void {
				var dataToPass:Object = new Object();
				dataToPass.title = "";
				dataToPass.text = comment;
				dataToPass.topText = resourceManager.getString('general','editnotetext');
				
				var passbackObject:Object = new Object();
				if (newExerciseEvent) {
					passbackObject.exerciseName = exerciseName;
					passbackObject.eventDateAndTime = eventDateAndTime;						
				} else {
					//updating the bg event, this means even if the user would hit the back button afterwards, the bgevent will be updated
					//this is in case user updates eg result or timing, then hits comment button, when coming back, we need to have the new values
					theExerciseEvent.updateExerciseEvent(exerciseName, eventDateAndTime.valueOf(),new Date().valueOf(),comment);
					passbackObject = theExerciseEvent;
				}
				dataToPass.dataToPassBack = passbackObject;
				navigator.pushView(TextPickerView,dataToPass);				
			}
			
			private function addHandler():void {
				if (!navigator.poppedViewReturnedObject) return; 
				if (!navigator.poppedViewReturnedObject.object) return;
				
				if (navigator.poppedViewReturnedObject.object.dataToPassBack) {
					if (navigator.poppedViewReturnedObject.object.dataToPassBack is ExerciseEvent) {
						theExerciseEvent = navigator.poppedViewReturnedObject.object.dataToPassBack as ExerciseEvent;
						exerciseName = theExerciseEvent.level;
						eventDateAndTime = new Date(theExerciseEvent.timeStamp);
						dateText = dateformatter.format(_eventDateAndTime);
						timeText = timeformatter.format(_eventDateAndTime);
						newExerciseEvent = false;
					} else
					{
						exerciseName = navigator.poppedViewReturnedObject.object.dataToPassBack.exerciseName as String;
						eventDateAndTime = navigator.poppedViewReturnedObject.object.dataToPassBack.eventDateAndTime as Date;
					}
					
					//if there's a datatopassback then there should also be a text
					//anyway let's check
					if (navigator.poppedViewReturnedObject.object.text != null)
						onTextSet(navigator.poppedViewReturnedObject.object.text as String);
				}
			}
			
			private function onTextSet(textSet:String): void
			{
				comment = textSet;
				if (newExerciseEvent) {
				} else {
					theExerciseEvent.updateExerciseEvent(exerciseName,eventDateAndTime.valueOf(),new Date().valueOf(),comment);
				}
			}
			
			private function dateClicked(e:MouseEvent = null):void {
				tempDate = eventDateAndTime.date;
				tempMonth = eventDateAndTime.month;
				tempYear = eventDateAndTime.fullYear;
				tempHour = eventDateAndTime.hours;
				tempMinute = eventDateAndTime.minutes;
				tempSeconds = eventDateAndTime.seconds;
				
				var dateTime:DateTimeDialogView = Dialog.service.create(
					new DateTimeDialogBuilder()
					.setMode(DialogType.MODE_DATE)
					.setTitle(ModelLocator.resourceManagerInstance.getString("general","date"))
					.setAcceptLabel("Ok")
					.setCancelable(true)
					.setCancelLabel(ModelLocator.resourceManagerInstance.getString('general','cancel'))
					.build()
				);
				dateTime.setDate(tempYear, tempMonth, tempDate);
				dateTime.setTime(tempHour, tempMinute);
				dateTime.addEventListener(DialogViewEvent.CLOSED, dateortime_closedRequestHandler);
				dateTime.addEventListener(DialogDateTimeEvent.SELECTED, date_selectedHandler);
				dateTime.show();
			}
			
			private function timeClicked(e:MouseEvent = null):void {
				tempDate = eventDateAndTime.date;
				tempMonth = eventDateAndTime.month;
				tempYear = eventDateAndTime.fullYear;
				tempHour = eventDateAndTime.hours;
				tempMinute = eventDateAndTime.minutes;
				tempSeconds = eventDateAndTime.seconds;
				
				var dateTime:DateTimeDialogView = Dialog.service.create(
					new DateTimeDialogBuilder()
					.setMode(DialogType.MODE_TIME)
					.setTitle(ModelLocator.resourceManagerInstance.getString("general","time"))
					.setAcceptLabel("Ok")
					.setCancelable(true)
					.setCancelLabel(ModelLocator.resourceManagerInstance.getString('general','cancel'))
					.build()
				);
				dateTime.setDate(tempYear, tempMonth, tempDate);
				dateTime.setTime(tempHour, tempMinute);
				dateTime.addEventListener(DialogViewEvent.CLOSED, dateortime_closedRequestHandler);
				dateTime.addEventListener(DialogDateTimeEvent.SELECTED, time_selectedHandler);
				dateTime.show();
			}
			
			private function time_selectedHandler( event:DialogDateTimeEvent ):void
			{
				tempDate = eventDateAndTime.date;
				tempMonth = eventDateAndTime.month;
				tempYear = eventDateAndTime.fullYear;
				tempHour = event.date.hours;
				tempMinute = event.date.minutes;
				tempSeconds = event.date.seconds;
			}
			
			private function date_selectedHandler( event:DialogDateTimeEvent ):void
			{
				tempDate = event.date.date;
				tempMonth = event.date.month;
				tempYear = event.date.fullYear;
				tempHour = eventDateAndTime.hours;
				tempMinute = eventDateAndTime.minutes;
				tempSeconds = eventDateAndTime.seconds;
			}
			
			private function dateortime_closedRequestHandler( event:DialogViewEvent ):void {
				var newDate:Date = new Date(tempYear, tempMonth, tempDate, tempHour, tempMinute, tempSeconds);
				eventDateAndTime = newDate;
			}

		]]>
	</fx:Script>
	<s:navigationContent>
		<s:Button  visible="false" label="{back_label}" id="backButton" click="navigator.popView()" height="48"/>
	</s:navigationContent>
	<s:actionContent>
		<s:Button icon="{ModelLocator.trashImage_48x48}" id="TrashButton" click="deleteEvent(event)"/>
		<s:Button icon="{ModelLocator.notesImage_48x48}" id="NotesButton" click="editNote(event)"/>
		<s:Button icon="{ModelLocator.doneImage_48x48}" id="OkButton" click="okClicked(event)"/>
	</s:actionContent>
	<s:Group y="0" x="0" width="100%" height = "100%" left="5" top="5" right="5">
		<s:layout>
			<s:VerticalLayout>
			</s:VerticalLayout>
		</s:layout>
		<s:Label y="0" x="0" width="100%" text="{resourceManager.getString('editexerciseeventview','addexerciseevent')}"  height="100%" maxHeight="75" textAlign="center" verticalAlign="middle"/>
		<s:Line width="100%">
			<s:stroke>
				<s:SolidColorStroke color="0xADADAD" weight="1"/>
			</s:stroke>
		</s:Line>	
		<s:Group id="exerciseGroup" y="0" x="0" width="100%" click="exerciseGroupClicked(event)">
			<s:layout>
				<s:HorizontalLayout verticalAlign="middle">
				</s:HorizontalLayout>
			</s:layout>
			<s:Label  id="intensity" height="47"  width="40%" text="{resourceManager.getString('editexerciseeventview','intensity')}"  verticalAlign="middle"/>
			<s:Button id="exerciseNameTextField" height="47" width="60%" label="@{exerciseName}" />
		</s:Group>
		<s:Line width="100%">
			<s:stroke>
				<s:SolidColorStroke color="0xADADAD" weight="1"/>
			</s:stroke>
		</s:Line>	
		<s:Group id="datumGroup" y="0" x="0" width="100%" click="dateClicked(event)">
			<s:layout>
				<s:HorizontalLayout verticalAlign="middle">
				</s:HorizontalLayout>
			</s:layout>
			<s:Label  id="datumtext" height="47" width="30%" text="{resourceManager.getString('general','date')}"  verticalAlign="middle"/>
			<s:Button id="datum" height="47" width="70%" label="@{dateText}" />
		</s:Group>
		<s:Line width="100%">
			<s:stroke>
				<s:SolidColorStroke color="0xADADAD" weight="1"/>
			</s:stroke>
		</s:Line>	
		<s:Group id="timeGroup" y="0" x="0" width="100%" click="timeClicked(event)">
			<s:layout>
				<s:HorizontalLayout verticalAlign="middle">
				</s:HorizontalLayout>
			</s:layout>
			<s:Label  id="timetext" height="47" width="30%" text="{resourceManager.getString('general','time')}"  verticalAlign="middle"/>
			<s:Button id="time" height="47" width="70%" label="@{timeText}" />
		</s:Group>
		<s:Line width="100%">
			<s:stroke>
				<s:SolidColorStroke color="0xADADAD" weight="1"/>
			</s:stroke>
		</s:Line>	
	</s:Group>
</s:View>
