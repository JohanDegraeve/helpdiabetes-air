<?xml version="1.0" encoding="utf-8"?>
<!--
Copyright (C) 2013  hippoandfriends

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/gpl.txt>.
-->
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		xmlns:mx="library://ns.adobe.com/flex/mx"
		xmlns:myComponents="myComponents.*" 
		creationComplete="creationCompleteHandler(event)"
		add="addHandler()"
		initialize="onInitialize()"
		>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Metadata>
		[ResourceBundle("trackingview")]
	</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.events.CollectionEvent;
			import mx.events.FlexEvent;
			
			import databaseclasses.BloodGlucoseEvent;
			import databaseclasses.ExerciseEvent;
			import databaseclasses.Meal;
			import databaseclasses.MealEvent;
			import databaseclasses.MedicinEvent;
			import databaseclasses.SelectedFoodItem;
			import databaseclasses.Settings;
			
			import model.ModelLocator;
			
			import myComponents.HelpCallOut;
			import myComponents.IListElement;
			import myComponents.PickerEvent;
			import myComponents.TrackingViewElement;
			
			import utilities.ExcelSorting;
			import utilities.FromtimeAndValueArrayCollection;
			import utilities.MyGATracker;
			
			private static var lastClickOnTrackingView:Number;
			private var helpCallOut:HelpCallOut;
			private var timer:Timer;
			
			[Bindable]
			private var activeInsulin_text:String;
			/**
			 * maximum insulin activity in milliseconds, initialized to 36 hours
			 */
			public static var maxInsulinActivity:Number = 129600000;
			
			private static var BOLUS_INTERVAL_FOR_SQUARE_WAVE_BOLUSSES:int = 1;//in minutes
			
			
			private function addHandler():void {
				if (!navigator.poppedViewReturnedObject) return; 
				if (!navigator.poppedViewReturnedObject.object) return;
				
				if (navigator.poppedViewReturnedObject.object.text != null)
					onTextSet(navigator.poppedViewReturnedObject.object.text as String);
			}
			
			private function onInitialize():void {
				title = resourceManager.getString('trackingview','logbook');
				
				lastClickOnTrackingView = (new Date()).valueOf();
				ModelLocator.getInstance().addEventListener(ModelLocator.SEARCHACTIVE_CHANGED,reDrawActionButtons);
			}
			
			private function reDrawActionButtons(event:Event = null):void {
				if (navigator) {
					if (navigator.actionBar.actionContent) {
						if (ModelLocator.getInstance().searchActive) {
							if (navigator.actionBar.actionContent.indexOf(searchButton) != -1)
								navigator.actionBar.actionGroup.removeElement(searchButton);
							if (ModelLocator.getInstance().firstMarkedItemEventId == ModelLocator.getInstance().lastMarkedItemEventId) {
								upButton.alpha = 0.25;
								downButton.alpha = 0.25
							} else if (ModelLocator.getInstance().lastMarkedItemEventId == ModelLocator.getInstance().trackingEventToShow) {
								downButton.alpha = 0.25;
								upButton.alpha = 1;
							} else if (ModelLocator.getInstance().firstMarkedItemEventId == ModelLocator.getInstance().trackingEventToShow) {
								upButton.alpha = 0.25;
								downButton.alpha = 1;
							} else  {
								upButton.alpha = 1;
								downButton.alpha = 1;
							}
							if (navigator.actionBar.actionContent.indexOf(upButton) == -1)  {
								navigator.actionBar.actionGroup.addElement(upButton);
								navigator.actionBar.actionGroup.addElement(downButton);
								navigator.actionBar.actionGroup.addElement(cancelButton);
							}
						} else  {
							if (navigator.actionBar.actionContent.indexOf(searchButton) == -1)  {
								navigator.actionBar.actionGroup.addElement(searchButton);
							}
							if (navigator.actionBar.actionContent.indexOf(upButton) != -1)  {
								navigator.actionBar.actionGroup.removeElement(upButton);
								navigator.actionBar.actionGroup.removeElement(downButton);
								navigator.actionBar.actionGroup.removeElement(cancelButton);
							}
						}
						openHelpTexts();
					} else {
						timer = new Timer(100, 1);
						timer.addEventListener(TimerEvent.TIMER,timerEnd);
						timer.start();
					}
				} else {
					timer = new Timer(100, 1);
					timer.addEventListener(TimerEvent.TIMER,timerEnd);
					timer.start();
				}
				
				function timerEnd():void {
					reDrawActionButtons();
				}
			}
			
			/**
			 * Will be used by views pushed by Trackingview (eg editbgeventview), if true then when the pushed view is opened, it will call poptofirstview<br>
			 * This is to force for instance, that when a user has opened trackingview, click on editbgeventview, more than 15 minutes later goes again to tracking view,
			 * to allow that automatically the trackingview will be shown again<br>
			 * To check how it is determined whether true or false, is returned, check the code of get popToFirstViewNecessary<br>
			 * It is necessary that the views that can be pushed by trackingview, call this method each time they are initialized and if pushed by trackingview, or reactivated ?
			 */
			public static function popToFirstViewNecessary():Boolean
				
			{
				if (isNaN(lastClickOnTrackingView)) {
					lastClickOnTrackingView = (new Date()).valueOf();
					return false;
				}
				if ((new Date()).valueOf() > lastClickOnTrackingView + 15 * 60 * 1000) {
					lastClickOnTrackingView = (new Date()).valueOf();
					return true;
				} 
				lastClickOnTrackingView = (new Date()).valueOf();
				return false;
			}
			
			private function getItemRenderer(item:Object):ClassFactory {
				return (item as IListElement).listElementRendererFunction();
			}
			
			private function onListElementClicked(event:MouseEvent):void {
				ModelLocator.getInstance().trackingEventToShow = (ModelLocator.getInstance().trackingList.getItemAt((event.currentTarget as List).selectedIndex) as TrackingViewElement).eventid;
				if (ModelLocator.getInstance().trackingList.getItemAt((event.currentTarget as List).selectedIndex) is MealEvent) {//be aware that the mealeventitemrenderer will stop propagation of this mouseevent if it was not extended before the item was clicked
					MyGATracker.getInstance().trackPageview( "TrackingView-MealEventClicked" );
					
					navigator.pushView(EditMealEventView,ModelLocator.getInstance().trackingList.getItemAt((event.currentTarget as List).selectedIndex)); 					
				} else if (ModelLocator.getInstance().trackingList.getItemAt((event.currentTarget as List).selectedIndex) is MedicinEvent) {
					MyGATracker.getInstance().trackPageview( "TrackingView-MedicinEventClicked" );
					navigator.pushView(EditMedicinEventView,ModelLocator.getInstance().trackingList.getItemAt((event.currentTarget as List).selectedIndex));
				} else if (ModelLocator.getInstance().trackingList.getItemAt((event.currentTarget as List).selectedIndex) is ExerciseEvent) {
					MyGATracker.getInstance().trackPageview( "TrackingView-ExerciseEventClicked" );
					navigator.pushView(EditExerciseEventView,ModelLocator.getInstance().trackingList.getItemAt((event.currentTarget as List).selectedIndex));
				} else if (ModelLocator.getInstance().trackingList.getItemAt((event.currentTarget as List).selectedIndex) is BloodGlucoseEvent) {
					MyGATracker.getInstance().trackPageview( "TrackingView-BGEventClicked" );
					navigator.pushView(EditBGEventView,ModelLocator.getInstance().trackingList.getItemAt((event.currentTarget as List).selectedIndex));
				}
			}
			
			private function searchClicked(e:MouseEvent = null):void {
				data = new Object();
				data.title = "";
				data.topText = resourceManager.getString('trackingview','searchforfood');
				data.helpTextSettingId = Settings.SettingsHelpTextTrackingView1;
				data.helpText = resourceManager.getString('trackingview','help_text_entersearchtext');
				navigator.pushView(TextPickerView,data);				
			}
			
			private function onTextSet(textSet:String): void
			{
				var cntr:int = 0;
				var selitemctr:int = 0;
				ModelLocator.getInstance().searchActive = false;
				ModelLocator.getInstance().firstMarkedItemEventId = 0;
				ModelLocator.getInstance().lastMarkedItemEventId = 0;
				
				if (textSet == "")
					return;
				var elementsFound:Boolean = false;
				var splittedText:Array = textSet.split(" ");
				//search the first text, as long as it's not an empty space
				var splittedTextCntr:int = 0;
				for (;splittedTextCntr < splittedText.length;splittedTextCntr++) {
					if (splittedText[splittedTextCntr] != "")
						break;
				}
				if (splittedTextCntr == splittedText.length)
					return;//there seem to be only space
				
				//start with the first substring
				
				for (cntr = 0;cntr < ModelLocator.getInstance().copyOfTrackingList.length;cntr++) {
					var allTextInOneString:String = "";
					if (ModelLocator.getInstance().copyOfTrackingList.getItemAt(cntr) is MealEvent) {
						var mealEvent:MealEvent = ModelLocator.getInstance().copyOfTrackingList.getItemAt(cntr) as MealEvent;
						for (selitemctr = 0;selitemctr < mealEvent.selectedFoodItems.length;selitemctr++) {
							allTextInOneString += " " + (mealEvent.selectedFoodItems.getItemAt(selitemctr) as SelectedFoodItem).itemDescription;
						}
						allTextInOneString += " " + mealEvent.comment;
					} else if (ModelLocator.getInstance().copyOfTrackingList.getItemAt(cntr) is ExerciseEvent) {
						var exerciseEvent:ExerciseEvent = ModelLocator.getInstance().copyOfTrackingList.getItemAt(cntr) as ExerciseEvent;
						allTextInOneString += " " + exerciseEvent.level;
						allTextInOneString += " " + exerciseEvent.comment;
					} else if (ModelLocator.getInstance().copyOfTrackingList.getItemAt(cntr) is BloodGlucoseEvent) {
						allTextInOneString += " " + (ModelLocator.getInstance().copyOfTrackingList.getItemAt(cntr) as BloodGlucoseEvent).comment;
					} else if (ModelLocator.getInstance().copyOfTrackingList.getItemAt(cntr) is MedicinEvent) {
						var medicinEvent:MedicinEvent = ModelLocator.getInstance().copyOfTrackingList.getItemAt(cntr) as MedicinEvent;
						allTextInOneString += " " + medicinEvent.medicinName;
						allTextInOneString += " " + medicinEvent.comment;
					} 
					splittedTextCntr = 0;
					while (splittedTextCntr < splittedText.length)
						if (ExcelSorting.stringAhasStringB(allTextInOneString,splittedText[splittedTextCntr] as String))
							splittedTextCntr++;
					else
					break;
					if (splittedTextCntr == splittedText.length) {
						(ModelLocator.getInstance().copyOfTrackingList.getItemAt(cntr) as TrackingViewElement).mark = true;
						ModelLocator.getInstance().lastMarkedItemEventId = (ModelLocator.getInstance().copyOfTrackingList.getItemAt(cntr) as TrackingViewElement).eventid;
						if (ModelLocator.getInstance().firstMarkedItemEventId == 0)
							ModelLocator.getInstance().firstMarkedItemEventId = ModelLocator.getInstance().lastMarkedItemEventId;
						elementsFound = true;
					}
				}
				
				(trackinglist.layout as TrackingViewLayout).firstUpdateDisplayList = true;
				ModelLocator.getInstance().trackingList.refresh();//forces a redraw
				
				if (elementsFound) {
					ModelLocator.getInstance().trackingEventToShow = ModelLocator.getInstance().lastMarkedItemEventId;
					ModelLocator.getInstance().searchActive = true;
				}
			}
			
			private function upClicked(e:MouseEvent = null):void {
				if (ModelLocator.getInstance().trackingEventToShow == ModelLocator.getInstance().firstMarkedItemEventId)
					return;
				//first find the location of the current eventtoshow
				var listctr:int = 0;
				for (listctr = 0;listctr < ModelLocator.getInstance().trackingList.length;listctr++) {
					if ((ModelLocator.getInstance().trackingList.getItemAt(listctr) as TrackingViewElement).eventid == ModelLocator.getInstance().trackingEventToShow)
						break;
				}
				//now go up again to find the next marked element
				listctr--;
				for (;listctr >= 0;listctr--) {
					if ((ModelLocator.getInstance().trackingList.getItemAt(listctr) as TrackingViewElement).mark) {
						ModelLocator.getInstance().trackingEventToShow = (ModelLocator.getInstance().trackingList.getItemAt(listctr) as TrackingViewElement).eventid;
						(trackinglist.layout as TrackingViewLayout).firstUpdateDisplayList = true;
						ModelLocator.getInstance().trackingList.refresh();//forces a redraw of the list
						reDrawActionButtons();
						break;
					}
				}
			}
			
			private function downClicked(e:MouseEvent = null):void {
				if (ModelLocator.getInstance().trackingEventToShow == ModelLocator.getInstance().lastMarkedItemEventId)
					return;
				//first find the location of the current eventtoshow
				var listctr:int = 0;
				for (listctr = 0;listctr < ModelLocator.getInstance().trackingList.length;listctr++) {
					if ((ModelLocator.getInstance().trackingList.getItemAt(listctr) as TrackingViewElement).eventid == ModelLocator.getInstance().trackingEventToShow)
						break;
				}
				//now go further down to find the next marked element
				listctr++;
				for (;listctr >= 0;listctr++) {
					if ((ModelLocator.getInstance().trackingList.getItemAt(listctr) as TrackingViewElement).mark) {
						ModelLocator.getInstance().trackingEventToShow = (ModelLocator.getInstance().trackingList.getItemAt(listctr) as TrackingViewElement).eventid;
						(trackinglist.layout as TrackingViewLayout).firstUpdateDisplayList = true;
						ModelLocator.getInstance().trackingList.refresh();//forces a redraw of the list
						reDrawActionButtons();
						break;
					}
				}
			}
			
			private function cancelClicked(e:MouseEvent = null):void {
				ModelLocator.getInstance().searchActive = false;
				ModelLocator.getInstance().firstMarkedItemEventId = 0;
				ModelLocator.getInstance().lastMarkedItemEventId = 0;
				ModelLocator.getInstance().trackingEventToShow = 
					(ModelLocator.getInstance().meals.getItemAt(ModelLocator.getInstance().selectedMeal) as Meal).mealEvent == null ? -1 :
					(ModelLocator.getInstance().meals.getItemAt(ModelLocator.getInstance().selectedMeal) as Meal).mealEvent.eventid;
				(trackinglist.layout as TrackingViewLayout).firstUpdateDisplayList = true;
				ModelLocator.getInstance().trackingList.refresh();
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void
				
			{
				reDrawActionButtons();
				if (!navigator) return;
				if (!navigator.actionBar.actionContent) return;
				if (navigator.actionBar.actionContent.indexOf(searchButton) != -1) {
					helpCallOut = new HelpCallOut();
					helpCallOut.helpText = resourceManager.getString('trackingview','help_text_searchbutton');
					helpCallOut.settingId = Settings.SettingsHelpTextTrackingViewSearchButton;
					//helpCallOut.addEventListener(PickerEvent.PICKER_SET,helptext0Removed);
					helpCallOut.open(searchButton,true)
				} /*else
				helptext0Removed();*/
				
				recalculateActiveInsulin();				
				ModelLocator.getInstance().copyOfTrackingList.addEventListener(CollectionEvent.COLLECTION_CHANGE,recalculateActiveInsulin);
			}
			
			private function recalculateActiveInsulin(event:Event = null):void  {
				//calculate activeInsulin_text
				var list:ArrayCollection = ModelLocator.getInstance().copyOfTrackingList;
				var activeInsulin:Number = new Number(0);
				var now:Date = new Date();
				for (var cntr:int = list.length - 1; cntr >= 0 ; cntr-- ) {
					//we go back maximum maxInsulinActivity
					if ((list.getItemAt(cntr) as TrackingViewElement).timeStamp + maxInsulinActivity < now.valueOf())
						break;
					if ((list.getItemAt(cntr) as TrackingViewElement).timeStamp < now.valueOf()) {//we don't include events in the future
						if (list.getItemAt(cntr) is MedicinEvent) {
							var theEvent:MedicinEvent = list.getItemAt(cntr) as MedicinEvent;
							//let's find if the name of the medicinevent matches one of the medicins in the settings
							for (var medicincntr:int = 0;medicincntr <  5;medicincntr++) {
								if (Settings.getInstance().getSetting( Settings.SettingsInsulinType1 + medicincntr) == theEvent.medicinName)  {
									if (Settings.getInstance().getSetting(Settings.SettingsMedicin1_AOBActive + medicincntr) == "true")  {
										//..zien welke range we moeten nemen
										var x_valueasString:String = (Settings.getInstance().getSetting(Settings.SettingsMedicin1_range1_AOBChart + medicincntr * 4).split("-")[0] as String).split(":")[1];
										var y_valueasString:String = (Settings.getInstance().getSetting(Settings.SettingsMedicin1_range2_AOBChart + medicincntr * 4).split("-")[0] as String).split(":")[1];
										var z_valueasString:String = (Settings.getInstance().getSetting(Settings.SettingsMedicin1_range3_AOBChart + medicincntr * 4).split("-")[0] as String).split(":")[1];
										var x_value:Number = Number(x_valueasString);
										var y_value:Number = Number(y_valueasString);
										var z_value:Number = Number(z_valueasString);
										var settingToUse:int;	
										if (theEvent.amount < x_value)
											settingToUse = Settings.SettingsMedicin1_range1_AOBChart + medicincntr * 4;
										else if (theEvent.amount < y_value)
											settingToUse = Settings.SettingsMedicin2_range1_AOBChart + medicincntr * 4;
										else if (theEvent.amount < z_value)
											settingToUse = Settings.SettingsMedicin3_range1_AOBChart + medicincntr * 4;
										else 
											settingToUse = Settings.SettingsMedicin4_range1_AOBChart + medicincntr * 4;
										var fromTimeAndValueArrayCollection:FromtimeAndValueArrayCollection = FromtimeAndValueArrayCollection.createList(Settings.getInstance().getSetting(settingToUse));
										if (theEvent.bolustype == resourceManager.getString('editmedicineventview','square')) {
											//split over 1 injection per minute
											var amountOfInjections:int = theEvent.bolusDuration / BOLUS_INTERVAL_FOR_SQUARE_WAVE_BOLUSSES;
											var bolusAmountPerInjectionAsInt:Number = theEvent.amount / amountOfInjections;
											var injectionsCntr:int;
											var timeStampOfInjection:Number;
											for (injectionsCntr = 0;injectionsCntr < amountOfInjections;injectionsCntr++) {
												timeStampOfInjection = ((list.getItemAt(cntr) as TrackingViewElement).timeStamp + injectionsCntr * BOLUS_INTERVAL_FOR_SQUARE_WAVE_BOLUSSES * 60 * 1000);
												if (timeStampOfInjection < now.valueOf()) {
													var percentage:Number = fromTimeAndValueArrayCollection.getValue((now.valueOf() - timeStampOfInjection)/1000);
													activeInsulin += bolusAmountPerInjectionAsInt *  percentage / 100;
												}
											}
											/*timeStampOfInjection = ((list.getItemAt(cntr) as TrackingViewElement).timeStamp + injectionsCntr * BOLUS_INTERVAL_FOR_SQUARE_WAVE_BOLUSSES * 60 * 1000);
											if (timeStampOfInjection < now.valueOf())
											activeInsulin += (theEvent.amount - bolusAmountPerInjectionAsInt * amountOfInjections)  * fromTimeAndValueArrayCollection.getValue((now.valueOf() - timeStampOfInjection)/1000) / 100;*/
										} else {
											activeInsulin += theEvent.amount * fromTimeAndValueArrayCollection.getValue((now.valueOf() - (list.getItemAt(cntr) as TrackingViewElement).timeStamp)/1000) / 100;
										}
									}
								}
							}
							
						}
					}
				}
				activeInsulin_text = 
					resourceManager.getString('trackingview','active_insulin')
					+ " " 
					+ ((Math.round(activeInsulin * 10))/10).toString() 
					+ " " 
					+ resourceManager.getString('trackingview','internationalunit');
			}
			
			/*private function helptext0Removed(event:Event = null):void {
			
			}*/
			
			/**
			 * helptexts related to up, down and cancel button 
			 */
			private function openHelpTexts():void {
				if (navigator.actionBar.actionContent.indexOf(upButton) != -1) {
					helpCallOut = new HelpCallOut();
					helpCallOut.helpText = resourceManager.getString('trackingview','help_text_upbutton');
					helpCallOut.settingId = Settings.SettingsHelpTextTrackingViewUpButton;
					helpCallOut.addEventListener(PickerEvent.PICKER_SET,helptext1Removed);
					helpCallOut.open(upButton,true)
				} else
					helptext1Removed();
			}
			
			private function helptext1Removed(event:Event = null):void  {
				if (navigator.actionBar.actionContent.indexOf(downButton) != -1) {
					helpCallOut = new HelpCallOut();
					helpCallOut.helpText = resourceManager.getString('trackingview','help_text_downbutton');
					helpCallOut.settingId = Settings.SettingsHelpTextTrackingViewDownButton;
					helpCallOut.addEventListener(PickerEvent.PICKER_SET,helptext2Removed);
					helpCallOut.open(downButton,true)
				} else
					helptext2Removed();
			}
			
			private function helptext2Removed(event:Event = null):void  {
				if (navigator.actionBar.actionContent.indexOf(cancelButton) != -1) {
					helpCallOut = new HelpCallOut();
					helpCallOut.helpText = resourceManager.getString('trackingview','help_text_cancelbutton');
					helpCallOut.settingId = Settings.SettingsHelpTextTrackingViewCancelButton;
					//helpCallOut.addEventListener(PickerEvent.PICKER_SET,helptext2Removed);
					helpCallOut.open(cancelButton,true)
				} /*else
				helptext2Removed();*/
			}
		]]>
	</fx:Script>
	<s:actionContent>
		<s:Button icon="@Embed(source='../assets/ic_up.png')" id="upButton" click="upClicked(event)"/>
		<s:Button icon="@Embed(source='../assets/ic_down.png')" id="downButton" click="downClicked(event)"/>
		<s:Button icon="@Embed(source='../assets/ic_menu_close_clear_cancel.png')" id="cancelButton" click="cancelClicked(event)" />
		<s:Button icon="@Embed(source='../assets/search48x48.png')" id="searchButton" click="searchClicked(event)"/>
	</s:actionContent>
		<s:Label id="activeInsulin" left="10" right="0" top="10" bottom="0" width="100%" text="{activeInsulin_text}" includeInLayout="{ModelLocator.getInstance().extendedFunctionsActive()}" visible="{ModelLocator.getInstance().extendedFunctionsActive()}"/>
		
		<s:List id="trackinglist" left="0" right="0" top="{ModelLocator.getInstance().extendedFunctionsActive() ? 40:0}" bottom="0" width="100%" useVirtualLayout="true" 
				dataProvider="{ModelLocator.getInstance().copyOfTrackingList}"
				click="onListElementClicked(event)"
				itemRendererFunction="getItemRenderer">
			<s:layout>
				<myComponents:TrackingViewLayout useVirtualLayout="true" id="trackinglistlayout"/>
			</s:layout>
		</s:List>
</s:View>
