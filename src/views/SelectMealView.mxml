<?xml version="1.0" encoding="utf-8"?>
<!--
Copyright (C) 2013  hippoandfriends

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/gpl.txt>.

-->
<!---
view to show ModelLocator.getInstance().meals in a list

-->
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		xmlns:mx="library://ns.adobe.com/flex/mx"
		xmlns:myComponents="myComponents.*"
		initialize="init()"
		creationComplete="onCreationComplete()" 
		add="addHandler()" >
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import com.google.analytics.AnalyticsTracker;
			
			import databaseclasses.Meal;
			
			import model.ModelLocator;
			
			import mx.events.FlexEvent;
			
			import myComponents.IListElement;
			
			import spark.events.IndexChangeEvent;
			
			import utilities.MyGATracker;
			[Bindable]
			private var back_label:String;

			public var tracker:AnalyticsTracker;
			private var trackerId:String;

			private function getItemRenderer(item:Object):ClassFactory {
				return (item as IListElement).listElementRendererFunction();
			}
			
			/**
			 * used as context value when pushing the view ChangeMealDateAndTimeView<br>
			 * when addHandler is called, the value of the return object will be check, if equal to CHANGE_MEAL_DATE_AND_TIME then view is popped
			 */
			private static const CHANGE_MEAL_DATE_AND_TIME:int = 1;
			/**
			 * used as context value when pushing the view addmealview<br>
			 * when addHandler is called, the value of the return object will be checked, if equal to  ADD_NEW_MEAL, then view is popped
			 */
			private static const ADD_NEW_MEAL:int=2
			
			private function init():void {
				trackerId = resourceManager.getString('analytics','trackeraccount');
			}
			
			private function onCreationComplete():void {
				back_label = resourceManager.getString('general','back');
				meallist.ensureIndexIsVisible(meallist.dataProvider.length -1);
				meallist.ensureIndexIsVisible(ModelLocator.getInstance().selectedMeal);
				ModelLocator.getInstance().addEventListener(ModelLocator.SELECTEDMEAL_CHANGED,onSelectedMealChanged);
				ModelLocator.getInstance().addEventListener(ModelLocator.SELECTEDMEAL_INITIALIZED,onSelectedMealChanged);
				meallist.addEventListener(Event.CHANGE,mealselected);
			}
			
			private function mealselected(event:IndexChangeEvent):void {
				
				if (tracker == null) {
					tracker = new MyGATracker( this, trackerId, "AS3", false );
				}
				tracker.trackPageview( "SelectMealView-mealSelectedFromList" );
				
				if (ModelLocator.getInstance().meals.getItemAt(event.newIndex)  is Meal) {
					//should we not do that also when ui is closed via back key ?
					ModelLocator.getInstance().removeEventListener(ModelLocator.SELECTEDMEAL_CHANGED,onSelectedMealChanged);
					ModelLocator.getInstance().removeEventListener(ModelLocator.SELECTEDMEAL_INITIALIZED,onSelectedMealChanged);
					meallist.removeEventListener(Event.CHANGE,mealselected);
					
					//if it's a future meal without mealevent, we'll ask to user to enter start date and time of the meal
					var theMeal:Meal = ModelLocator.getInstance().meals.getItemAt(event.newIndex) as Meal;
					if (theMeal.hasMealEvent()) {
						ModelLocator.getInstance().selectedMeal = event.newIndex;
						navigator.popView();
					} else {
						navigator.pushView(ChangeMealDateAndTimeView,{
							meal:theMeal
						},CHANGE_MEAL_DATE_AND_TIME);
					}
				}
			}
			
			private function onSelectedMealChanged(e:Event):void {
				try {//doing this because sometimes an exception was thrown here, not sure why
					meallist.ensureIndexIsVisible(meallist.dataProvider.length -1);
				} catch (error:Error) {
					trace("exception thrown in SelectedMealView.onSelectedMeall Changed - not important");
					;
				}
				meallist.ensureIndexIsVisible(ModelLocator.getInstance().selectedMeal);
			}
			
			/**
			 * will check if navigator.poppedViewReturnedObject is not null and if so, checks the value<br>
			 * if equal to CHANGE_MEAL_DATE_AND_TIME or ADD_NEW_MEAL then view is popped.
			 */
			private function addHandler():void {
				if (navigator.poppedViewReturnedObject != null) {
					if (navigator.poppedViewReturnedObject.context == CHANGE_MEAL_DATE_AND_TIME || navigator.poppedViewReturnedObject.context == ADD_NEW_MEAL) {
						if (navigator.poppedViewReturnedObject.object != null) {
							ModelLocator.getInstance().selectedMeal = navigator.poppedViewReturnedObject.object as Number;
							navigator.popView();
						}
					} 
				}
			}
			
			/**
			 * will return navigator.context
			 */
			override public function createReturnObject():Object {
				return navigator.context;
			}
			
			private function addButtonClicked(event:MouseEvent):void {
				ModelLocator.getInstance().removeEventListener(ModelLocator.SELECTEDMEAL_CHANGED,onSelectedMealChanged);
				ModelLocator.getInstance().removeEventListener(ModelLocator.SELECTEDMEAL_INITIALIZED,onSelectedMealChanged);

				navigator.pushView(AddMealView,null,ADD_NEW_MEAL);
			}

			
		]]>
	</fx:Script>
	<s:navigationContent>
		<s:Button   label="{back_label}" id="backButton" click="navigator.popView()" height="48"/>
	</s:navigationContent>

	<s:actionContent>
		<s:Button icon="@Embed(source='../assets/add48x48.png')" click="addButtonClicked(event)"/>
	</s:actionContent>
	<s:List id="meallist" left="0" right="0" top="0" bottom="0" width="100%"
			dataProvider="{ModelLocator.getInstance().meals}" 
			itemRendererFunction="getItemRenderer">
	</s:List>
</s:View>
