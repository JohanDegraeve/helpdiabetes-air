<?xml version="1.0" encoding="utf-8"?>
<!--
Copyright (C) 2013  hippoandfriends

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/gpl.txt>.

-->
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" title=""
		initialize="onInitialize()"
		add="addHandler()"
		creationComplete="onCreationComplete()" actionBarVisible="true"
		backKeyPressed="onBackKeyPressed(event)">
	<fx:Declarations>
		<s:DateTimeFormatter id="dateformatter" dateTimePattern="{resourceManager.getString('general','datepattern')}" useUTC="false" locale="{Capabilities.language.substr(0,2)}"/>
		<s:DateTimeFormatter id ="timeformatter" dateTimePattern="{resourceManager.getString('general','timepattern')}" useUTC="false"/>
	</fx:Declarations>
	<fx:Metadata>
		[ResourceBundle("editmedicineventview")]
		[ResourceBundle("editbgeventview")]
	</fx:Metadata>
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.core.UIComponent;
			import mx.managers.PopUpManager;
			
			import spark.components.TabbedViewNavigator;
			import spark.components.ViewNavigator;
			
			import databaseclasses.BloodGlucoseEvent;
			import databaseclasses.ExerciseEvent;
			import databaseclasses.MedicinEvent;
			import databaseclasses.Settings;
			import databaseclasses.UnitChangedEvent;
			
			import model.ModelLocator;
			
			import myComponents.AmountPicker;
			import myComponents.DatePicker;
			import myComponents.DayLineWithTotalAmount;
			import myComponents.HelpCallOut;
			import myComponents.PickerEvent;
			import myComponents.RequestConfirmation;
			import myComponents.TimePicker;
			import myComponents.TrackingViewElement;
			import myComponents.elementFromListPicker;
			
			import utilities.MyGATracker;
			import utilities.Synchronize;
			
			private var amountPicker:AmountPicker;
			private var medicinPicker:elementFromListPicker;
			private var dateOrTimePicker:Group;
			private var confirmationPicker:RequestConfirmation;

			[Bindable]
			private var medicinName:String;
			[Bindable]
			private var dateText:String ;
			[Bindable]
			private var timeText:String;
			[Bindable]
			private var amountText:String = "0";
			[Bindable]
			private var back_label:String;

			private var comment:String;
			
			/**
			 * if true then this view is created for a new mealevent 
			 */
			private var newMedicinEvent:Boolean;
			
			/**
			 * stores the medicinevent in case it's an existing event that is being modified, used also in case a new event is created 
			 */
			private var theMedicinEvent:MedicinEvent;

			private var helpCallOut:HelpCallOut;

			private var _eventDateAndTime:Date;
			private function get eventDateAndTime():Date
			{
				return _eventDateAndTime;
			}
			private function set eventDateAndTime(value:Date):void
			{
				_eventDateAndTime = value;
				dateText = dateformatter.format(_eventDateAndTime);
				timeText = timeformatter.format(_eventDateAndTime);
			}
			
			private function onCreationComplete():void {
				if (data is MedicinEvent) {
					if (TrackingView.popToFirstViewNecessary())
						navigator.popToFirstView();
					else {
						theMedicinEvent = data as MedicinEvent;
						eventDateAndTime = new Date((data as MedicinEvent).timeStamp);
						dateText = dateformatter.format(_eventDateAndTime);
						timeText = timeformatter.format(_eventDateAndTime);
						medicinName = (data as MedicinEvent).medicinName;
						comment = theMedicinEvent.comment;
						amountText = (new Number((data as MedicinEvent).amount)).toString();
						newMedicinEvent = false;
						backButton.visible=true;
						TrashButton.visible = true;
					}
				} else {
					helpCallOut = new HelpCallOut();
					helpCallOut.helpText = resourceManager.getString('editmedicineventview','help_text_OkButton');
					helpCallOut.settingId = Settings.SettingsHelpTextEditMedicinEventViewOkButton;
					helpCallOut.addEventListener(PickerEvent.PICKER_SET,helptext0Removed);
					helpCallOut.open(OkButton,true);
				}
			}
			
			private function helptext0Removed(event:Event):void {
				helpCallOut.removeEventListener(PickerEvent.PICKER_SET,helptext0Removed);
				helpCallOut = new HelpCallOut();
				helpCallOut.helpText = resourceManager.getString('editmedicineventview','help_text_selectMedicin');
				helpCallOut.settingId = Settings.SettingsHelpTextEditMedicinEventViewChangeMedicinType;
				helpCallOut.addEventListener(PickerEvent.PICKER_SET,helptext1Removed);
				helpCallOut.open(medicinNameTextField,true);
			}
			
			private function helptext1Removed(event:Event):void {
				helpCallOut.removeEventListener(PickerEvent.PICKER_SET,helptext1Removed);
				helpCallOut = new HelpCallOut();
				helpCallOut.helpText = resourceManager.getString('editmedicineventview','help_text_choseAmount');
				helpCallOut.settingId = Settings.SettingsHelpTextEditMedicinEventViewChangeAmount;
				helpCallOut.addEventListener(PickerEvent.PICKER_SET,helptext2Removed);
				helpCallOut.open(amount,true);
			}
			
			private function helptext2Removed(event:Event):void {
				helpCallOut.removeEventListener(PickerEvent.PICKER_SET,helptext2Removed);
				helpCallOut = new HelpCallOut();
				helpCallOut.helpText = resourceManager.getString('editbgeventview','help_text_changeDateAndTime');
				helpCallOut.settingId = Settings.SettingsHelpTextEditBGEventViewChangeDateAndTime;
				helpCallOut.addEventListener(PickerEvent.PICKER_SET,helptext3Removed);
				helpCallOut.open(timeGroup,true)
			}
			
			private function helptext3Removed(event:Event):void {
				helpCallOut.removeEventListener(PickerEvent.PICKER_SET,helptext3Removed);
			}
			
			private function onBackKeyPressed(event:Event):void {
				if (medicinPicker != null) {
					if (medicinPicker.isPopUp) {
						PopUpManager.removePopUp(medicinPicker);
						event.preventDefault();
					}
					medicinPicker = null;
				} 
				if (dateOrTimePicker != null) {
					if (dateOrTimePicker.isPopUp) {
						PopUpManager.removePopUp(dateOrTimePicker);
						event.preventDefault();
					}
					dateOrTimePicker = null;
				} 
				if (amountPicker != null) {
					if (amountPicker.isPopUp) {
						PopUpManager.removePopUp(amountPicker);
						event.preventDefault();
					}
					amountPicker = null;
				} 
				if (confirmationPicker != null) {
					if (confirmationPicker.isPopUp) {
						PopUpManager.removePopUp(confirmationPicker);
						event.preventDefault();
					}
				}
			}

			private function onInitialize():void {
				//setting initial values, will be overwritten in oncreationcomplete if data is an medicinevent
				eventDateAndTime = new Date();
				dateText = dateformatter.format(_eventDateAndTime);
				timeText = timeformatter.format(_eventDateAndTime);
				medicinName = Settings.getInstance().getSetting(parseInt(Settings.getInstance().getSetting(Settings.SettingsDefaultMedicin)));
				back_label = resourceManager.getString('general','back');
				newMedicinEvent = true;
				TrashButton.visible = false;
			}

			private function insulinTypeGroupClicked(e:MouseEvent = null):void {
				var unitList:ArrayCollection = new ArrayCollection();
				for (var i:int = Settings.SettingsInsulinType1; i <= Settings.SettingsInsulinType5;i++) {
					if (Settings.getInstance().getSetting(i) != null)
						if (Settings.getInstance().getSetting(i) != "")
							unitList.addItem(Settings.getInstance().getSetting(i));
				}
				
				medicinPicker = new elementFromListPicker();
				medicinPicker.addEventListener(UnitChangedEvent.ITEM_SELECTED, medicinSelected);
				medicinPicker.elements = unitList;
				medicinPicker.labelText1 = resourceManager.getString('editmedicineventview','select_medicin');
				
				PopUpManager.addPopUp(medicinPicker,UIComponent(this.parentApplication),true);
				PopUpManager.centerPopUp(medicinPicker);

				function medicinSelected(event:UnitChangedEvent):void {
					medicinPicker.removeEventListener(UnitChangedEvent.ITEM_SELECTED,medicinSelected);
					PopUpManager.removePopUp(medicinPicker);
					medicinName = Settings.getInstance().getSetting(Settings.SettingsInsulinType1 + event.index);
				}
			}
			
			private function amountGroupClicked(e:MouseEvent = null):void {
				amountPicker = new AmountPicker();
				amountPicker.amount = amountText;
				amountPicker.maxNonDecimalDigits = 2;
				amountPicker.decimalDigits = 1;
				amountPicker.labelText1 = resourceManager.getString('editmedicineventview','amount_of_units');
				amountPicker.addEventListener(PickerEvent.PICKER_SET, onAmountSet);
				amountPicker.addEventListener(PickerEvent.PICKER_CANCEL, onAmountCancel);
				
				PopUpManager.addPopUp(amountPicker, this, true);
				PopUpManager.centerPopUp(amountPicker);
				
				function onAmountCancel(event: PickerEvent): void
				{
					amountPicker.removeEventListener(PickerEvent.PICKER_SET, onAmountSet);
					amountPicker.removeEventListener(PickerEvent.PICKER_CANCEL, onAmountCancel);
				}
				
				function onAmountSet(event: PickerEvent): void
				{
					amountPicker.removeEventListener(PickerEvent.PICKER_SET, onAmountSet);
					amountPicker.removeEventListener(PickerEvent.PICKER_CANCEL, onAmountCancel);
					amountText = (event.newValue as Number).toString();
				}
			}
			
			private function datumClicked(e:MouseEvent = null):void {
				dateOrTimePicker  = new DatePicker();
				(dateOrTimePicker as DatePicker).currentDate = eventDateAndTime;
				dateOrTimePicker.addEventListener(PickerEvent.PICKER_SET, onTimeOrDateSet);
				dateOrTimePicker.addEventListener(PickerEvent.PICKER_CANCEL, onTimeOrDateCancel);
				PopUpManager.addPopUp(dateOrTimePicker, this, true);
				PopUpManager.centerPopUp(dateOrTimePicker);
			}
			
			private function onTimeOrDateCancel(event: PickerEvent): void
			{
				dateOrTimePicker.removeEventListener(PickerEvent.PICKER_SET, onTimeOrDateSet);
				dateOrTimePicker.removeEventListener(PickerEvent.PICKER_CANCEL, onTimeOrDateCancel);
			}
			
			private function onTimeOrDateSet(event: PickerEvent): void
			{
				dateOrTimePicker.removeEventListener(PickerEvent.PICKER_SET, onTimeOrDateSet);
				dateOrTimePicker.removeEventListener(PickerEvent.PICKER_CANCEL, onTimeOrDateCancel);
				eventDateAndTime = (event.newValue as Date);
			}
			
			private function timeClicked(e:MouseEvent = null):void {
				dateOrTimePicker = new TimePicker();
				(dateOrTimePicker as TimePicker).currentTime = eventDateAndTime;
				dateOrTimePicker.addEventListener(PickerEvent.PICKER_SET, onTimeOrDateSet);
				dateOrTimePicker.addEventListener(PickerEvent.PICKER_CANCEL, onTimeOrDateCancel);
				PopUpManager.addPopUp(dateOrTimePicker, this, true);
				PopUpManager.centerPopUp(dateOrTimePicker);
			}
			
			private function okClicked(e:MouseEvent = null):void {
				MyGATracker.getInstance().trackPageview( "EditMedicinEventView-OkClicked" );

				if (newMedicinEvent) {
					theMedicinEvent = new MedicinEvent(
						new Number(amountText),
						medicinName,
						new Date().valueOf(),
						comment,//comment,
						eventDateAndTime.valueOf());
					ModelLocator.getInstance().trackingList.addItem(theMedicinEvent);
				} else {
					theMedicinEvent.updateMedicinEvent(medicinName,new Number(amountText),comment,eventDateAndTime.valueOf(),new Date().valueOf());
				}
				
				var creationTimeStampAsDate:Date = new Date(theMedicinEvent.timeStamp);
				creationTimeStampAsDate.setHours(0,0,0,0);
				if (creationTimeStampAsDate.valueOf() > ModelLocator.getInstance().oldestDayLineStoredInTrackingList) {
					for (var i:Number = creationTimeStampAsDate.valueOf(); i >  (ModelLocator.getInstance().oldestDayLineStoredInTrackingList + 1); i = i - 86400000) {
						ModelLocator.getInstance().trackingList.addItem(new DayLineWithTotalAmount(i));
						if (ModelLocator.getInstance().youngestDayLineStoredInTrackingList == 5000000000000)
							i =  ModelLocator.getInstance().oldestDayLineStoredInTrackingList;//we don't need to do that 
					}
					ModelLocator.getInstance().trackingList.refresh();
					ModelLocator.getInstance().oldestDayLineStoredInTrackingList = creationTimeStampAsDate.valueOf();
					if (ModelLocator.getInstance().youngestDayLineStoredInTrackingList == 5000000000000)
						ModelLocator.getInstance().youngestDayLineStoredInTrackingList = creationTimeStampAsDate.valueOf();
				}
				else
					if (creationTimeStampAsDate.valueOf() < ModelLocator.getInstance().youngestDayLineStoredInTrackingList) {
						for (var j:Number = creationTimeStampAsDate.valueOf(); j <  ModelLocator.getInstance().youngestDayLineStoredInTrackingList;j = j + 86400000) {
							ModelLocator.getInstance().trackingList.addItem(new DayLineWithTotalAmount(j));
							if (ModelLocator.getInstance().oldestDayLineStoredInTrackingList == 0)
								j = ModelLocator.getInstance().youngestDayLineStoredInTrackingList;
						}
						ModelLocator.getInstance().trackingList.refresh();
						ModelLocator.getInstance().youngestDayLineStoredInTrackingList = creationTimeStampAsDate.valueOf();
						if (ModelLocator.getInstance().oldestDayLineStoredInTrackingList == 0)
							ModelLocator.getInstance().oldestDayLineStoredInTrackingList = creationTimeStampAsDate.valueOf();
					}
				
				ModelLocator.getInstance().trackingList.refresh();
				//switch to the tracking view and set trackingeventtoshow to this event
				ModelLocator.getInstance().trackingEventToShow = theMedicinEvent.eventid;
				(navigator.parentNavigator as TabbedViewNavigator).selectedIndex = 1;
				((navigator.parentNavigator as TabbedViewNavigator).navigators[1] as ViewNavigator).popToFirstView();
				Synchronize.getInstance().startSynchronize(true,false);
			}
			
			private function deleteEvent(e:MouseEvent = null):void {
				confirmationPicker = new RequestConfirmation();
				confirmationPicker.labelText1 = resourceManager.getString('general','delete_event');
				confirmationPicker.addEventListener(PickerEvent.PICKER_SET, onYesClicked);
				confirmationPicker.addEventListener(PickerEvent.PICKER_CANCEL, onNoClicked);
				
				PopUpManager.addPopUp(confirmationPicker, this, true);
				PopUpManager.centerPopUp(confirmationPicker);
				
				function onYesClicked(event: PickerEvent): void
				{
					MyGATracker.getInstance().trackPageview( "EditMedicinEventView-deleteClicked" );

					confirmationPicker.removeEventListener(PickerEvent.PICKER_SET, onYesClicked);
					confirmationPicker.removeEventListener(PickerEvent.PICKER_CANCEL, onNoClicked);
					Synchronize.getInstance().addObjectToBeDeleted(theMedicinEvent);
					Synchronize.getInstance().startSynchronize(true,false);

					theMedicinEvent.deleteEvent();
					//we need to make sure the previous tracking event is visible so we store the itemindex
					var trackinglistPointer:Number = ModelLocator.getInstance().trackingList.getItemIndex(theMedicinEvent);
					//now delete it
					ModelLocator.getInstance().trackingList.removeItemAt(trackinglistPointer);
					//and now make sure the previous item is visible
					if (trackinglistPointer > 0)
						ModelLocator.getInstance().trackingEventToShow = 
							(ModelLocator.getInstance().trackingList.getItemAt(trackinglistPointer - 1) as TrackingViewElement).eventid;						
					//switch to the tracking view
					(navigator.parentNavigator as TabbedViewNavigator).selectedIndex = 1;
					((navigator.parentNavigator as TabbedViewNavigator).navigators[1] as ViewNavigator).popToFirstView();
				}
				
				function onNoClicked(event: PickerEvent): void
				{
					confirmationPicker.removeEventListener(PickerEvent.PICKER_SET, onYesClicked);
					confirmationPicker.removeEventListener(PickerEvent.PICKER_CANCEL, onNoClicked);
				}
			}
			
			private function editNote(e:MouseEvent = null):void {
				var dataToPass:Object = new Object();
				dataToPass.title = "";
				dataToPass.text = comment;
				dataToPass.topText = resourceManager.getString('general','editnotetext');
				
				var passbackObject:Object = new Object();
				if (newMedicinEvent) {
					passbackObject.medicinName = medicinName;
					passbackObject.amountText = amountText;
					passbackObject.eventDateAndTime = eventDateAndTime;						
				} else {
					//updating the bg event, this means even if the user would hit the back button afterwards, the bgevent will be updated
					//this is in case user updates eg result or timing, then hits comment button, when coming back, we need to have the new values
					theMedicinEvent.updateMedicinEvent(medicinName,new Number(amountText), comment, eventDateAndTime.valueOf(),new Date().valueOf());
					passbackObject = theMedicinEvent;
				}
				dataToPass.dataToPassBack = passbackObject;
				navigator.pushView(TextPickerView,dataToPass);				
			}
			
			private function addHandler():void {
				if (!navigator.poppedViewReturnedObject) return; 
				if (!navigator.poppedViewReturnedObject.object) return;
				
				if (navigator.poppedViewReturnedObject.object.dataToPassBack) {
					if (navigator.poppedViewReturnedObject.object.dataToPassBack is MedicinEvent) {
						theMedicinEvent = navigator.poppedViewReturnedObject.object.dataToPassBack as MedicinEvent;
						medicinName = theMedicinEvent.medicinName;
						amountText = (new  Number(theMedicinEvent.amount)).toString();
						eventDateAndTime = new Date(theMedicinEvent.timeStamp);
						dateText = dateformatter.format(_eventDateAndTime);
						timeText = timeformatter.format(_eventDateAndTime);
						newMedicinEvent = false;
					} else
					{
						medicinName = navigator.poppedViewReturnedObject.object.dataToPassBack.medicinName as String;
						amountText = navigator.poppedViewReturnedObject.object.dataToPassBack.amountText as String;
						eventDateAndTime = navigator.poppedViewReturnedObject.object.dataToPassBack.eventDateAndTime as Date;
					}
					
					if (navigator.poppedViewReturnedObject.object.text != null)
						onTextSet(navigator.poppedViewReturnedObject.object.text as String);
				}
			}
			
			private function onTextSet(textSet:String): void
			{
				comment = textSet;
				if (newMedicinEvent) {
				} else {
					theMedicinEvent.updateMedicinEvent(medicinName,new Number(amountText),comment,eventDateAndTime.valueOf(),new Date().valueOf());
				}
			}
		]]>
	</fx:Script>
	<s:navigationContent>
		<s:Button  visible="false" label="{back_label}" id="backButton" click="navigator.popView()" height="48"/>
	</s:navigationContent>
	<s:actionContent>
		<s:Button icon="@Embed(source='../assets/Trash_48x48.png')" id="TrashButton" click="deleteEvent(event)"/>
		<s:Button icon="@Embed(source='../assets/Notes_48x48.png')" id="NotesButton" click="editNote(event)"/>
		<s:Button icon="@Embed(source='../assets/Done_48x48.png')" id="OkButton" click="okClicked(event)"/>
	</s:actionContent>

	<s:Group y="0" x="0" width="100%" height = "100%" left="5" top="5" right="5">
		<s:layout>
			<s:VerticalLayout >
			</s:VerticalLayout>
		</s:layout>
		<s:Label y="0" x="0" width="100%" text="{resourceManager.getString('editmedicineventview','addmedicinevent')}" height="100%" maxHeight="75"  textAlign="center" verticalAlign="middle"/>
		<s:Line width="100%">
			<s:stroke>
				<s:SolidColorStroke color="0xcccccc" weight="1"/>
			</s:stroke>
		</s:Line>	
		<s:Group id="insulinTypeGroup" y="0" x="0" width="100%" click="insulinTypeGroupClicked(event)">
			<s:layout>
				<s:HorizontalLayout verticalAlign="middle">
				</s:HorizontalLayout>
			</s:layout>
			<s:Label  id="insulinTypeText" height="47" width="40%" text="{resourceManager.getString('editmedicineventview','medicin')}" verticalAlign="middle"/>
			<s:Button id="medicinNameTextField" height="47" width="60%" label="@{medicinName}" />
		</s:Group>
		<s:Line width="100%">
			<s:stroke>
				<s:SolidColorStroke color="0xcccccc" weight="1"/>
			</s:stroke>
		</s:Line>	
		<s:Group id="amountGroup"  y="0" x="0" width="100%" click="amountGroupClicked(event)">
			<s:layout>
				<s:HorizontalLayout verticalAlign="middle">
				</s:HorizontalLayout>
			</s:layout>
			<s:Label  id="amountGroupText" height="47" width="70%" text="{resourceManager.getString('editmedicineventview','amount')}"  verticalAlign="middle"/>
			<s:Button id="amount" height="47" width="30%" label="@{amountText}" />
		</s:Group>
		<s:Line width="100%">
			<s:stroke>
				<s:SolidColorStroke color="0xcccccc" weight="1"/>
			</s:stroke>
		</s:Line>	
		<s:Group id="datumGroup" y="0" x="0" width="100%" click="datumClicked(event)">
			<s:layout>
				<s:HorizontalLayout verticalAlign="middle">
				</s:HorizontalLayout>
			</s:layout>
			<s:Label  id="datumtext" height="47" width="30%" text="{resourceManager.getString('general','date')}"  verticalAlign="middle"/>
			<s:Button id="datum" height="47" width="70%" label="@{dateText}" />
		</s:Group>
		<s:Line width="100%">
			<s:stroke>
				<s:SolidColorStroke color="0xcccccc" weight="1"/>
			</s:stroke>
		</s:Line>	
		<s:Group id="timeGroup" y="0" x="0" width="100%" click="timeClicked(event)">
			<s:layout>
				<s:HorizontalLayout verticalAlign="middle">
				</s:HorizontalLayout>
			</s:layout>
			<s:Label  id="timetext" height="47" width="30%" text="{resourceManager.getString('general','time')}"  verticalAlign="middle"/>
			<s:Button id="time" height="47" width="70%" label="@{timeText}" />
		</s:Group>
		<s:Line width="100%">
			<s:stroke>
				<s:SolidColorStroke color="0xcccccc" weight="1"/>
			</s:stroke>
		</s:Line>	
	</s:Group>
</s:View>
