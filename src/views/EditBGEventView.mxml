<?xml version="1.0" encoding="utf-8"?>
<!--
Copyright (C) 2016  hippoandfriends

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/gpl.txt>.

-->
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" title=""
		backKeyPressed="onBackKeyPressed(event)"  
		creationComplete="onCreationComplete()"
		add="addHandler()"
		actionBarVisible="true"
		initialize="onInitialize()">
	<fx:Declarations>
		<s:DateTimeFormatter id="dateformatter" dateTimePattern="{resourceManager.getString('general','datepattern')}" useUTC="false" locale="{Capabilities.language.substr(0,2)}"/>
		<s:DateTimeFormatter id ="timeformatter" dateTimePattern="{resourceManager.getString('general','timepattern')}" useUTC="false"/>
	</fx:Declarations>
	<fx:Metadata>
		[ResourceBundle("editbgeventview")]
		[ResourceBundle("general")]
	</fx:Metadata>
	<fx:Script>
		<![CDATA[
			import com.distriqt.extension.dialog.DateTimeDialogView;
			import com.distriqt.extension.dialog.Dialog;
			import com.distriqt.extension.dialog.DialogType;
			import com.distriqt.extension.dialog.builders.DateTimeDialogBuilder;
			import com.distriqt.extension.dialog.events.DialogDateTimeEvent;
			import com.distriqt.extension.dialog.events.DialogViewEvent;
			
			import flash.utils.setTimeout;
			
			import mx.managers.PopUpManager;
			
			import spark.components.TabbedViewNavigator;
			import spark.components.ViewNavigator;
			
			import databaseclasses.BloodGlucoseEvent;
			import databaseclasses.MealEvent;
			import databaseclasses.Settings;
			
			import model.ModelLocator;
			
			import myComponents.AmountPicker;
			import myComponents.DatePicker;
			import myComponents.DayLineWithTotalAmount;
			import myComponents.HelpCallOut;
			import myComponents.PickerEvent;
			import myComponents.RequestConfirmation;
			import myComponents.TimePicker;
			import myComponents.TrackingViewElement;
			
			import utilities.DateTimeUtilities;
			import utilities.MyGATracker;
			import utilities.Synchronize;
			import utilities.Trace;
			
			private var confirmationPicker:RequestConfirmation;
			private var theBloodGlucoseEvent:BloodGlucoseEvent;
			private var dateOrTimePicker:Group;
			private var _eventDateAndTime:Date;
			private function get eventDateAndTime():Date
			{
				return _eventDateAndTime;
			}
			private function set eventDateAndTime(value:Date):void
			{
				_eventDateAndTime = value;
				dateText = dateformatter.format(_eventDateAndTime);
				timeText = timeformatter.format(_eventDateAndTime);
			}
			
			private var unit:String;
			
			private var amountPicker:AmountPicker;
			[Bindable]
			private var meteringResultText:String;
			[Bindable]
			private var dateText:String ;
			[Bindable]
			private var timeText:String;
			[Bindable]
			private var back_label:String;
			private var comment:String;
			
			/**
			 * if true then this view is created for a new mealevent 
			 */
			private var newBloodGlucoseEvent:Boolean;
			
			
			[Bindable]
			private var meteringTexttext:String;
			
			private var helpCallOut:HelpCallOut;

			private var tempDate:int = 0;
			private var tempMonth:int = 0;
			private var tempYear:int = 0;
			private var tempHour:int = 0;
			private var tempMinute:int = 0;
			private var tempSeconds:int = 0;

			private function onCreationComplete(): void {
				back_label = resourceManager.getString('general','back');
				if (data is BloodGlucoseEvent) {//coming from trackingview
					if (TrackingView.popToFirstViewNecessary())
						navigator.popToFirstView();
					else {
						theBloodGlucoseEvent = data as BloodGlucoseEvent;
						eventDateAndTime = new Date(theBloodGlucoseEvent.timeStamp);
						dateText = dateformatter.format(_eventDateAndTime);
						timeText = timeformatter.format(_eventDateAndTime);
						unit = theBloodGlucoseEvent.unit;
						comment = theBloodGlucoseEvent.comment;
						meteringTexttext = resourceManager.getString('editbgeventview','value') + ' (' + unit + ')';
						meteringResultText = (new Number(theBloodGlucoseEvent.bloodGlucoseLevel)).toString();
						newBloodGlucoseEvent = false;
						backButton.visible=true;
						TrashButton.visible = true;
					}
				} else  {
					if (Settings.getInstance().getSetting(Settings.SettingsHelpTextEditBGEventViewOkButton) == 'true') {
						helpCallOut = new HelpCallOut();
						helpCallOut.helpText = resourceManager.getString('editbgeventview','help_text_OkButton');
						helpCallOut.settingId = Settings.SettingsHelpTextEditBGEventViewOkButton;
						helpCallOut.addEventListener(PickerEvent.PICKER_SET,helptext0Removed);
						helpCallOut.open(OkButton,true)
					} else {
						helptext0Removed(null);
					}
				}
			}
			
			private function helptext0Removed(event:Event):void {
				if (helpCallOut != null) helpCallOut.removeEventListener(PickerEvent.PICKER_SET,helptext0Removed);
				if (Settings.getInstance().getSetting(Settings.SettingsHelpTextEditBGEventViewChangeAmount) == 'true') {
					helpCallOut = new HelpCallOut();
					helpCallOut.helpText = resourceManager.getString('editbgeventview','help_text_selectChangeBGAmount');
					helpCallOut.settingId = Settings.SettingsHelpTextEditBGEventViewChangeAmount;
					helpCallOut.addEventListener(PickerEvent.PICKER_SET,helptext1Removed);
					helpCallOut.open(meteringResult,true);
				} else {
					helptext1Removed(null);
				}
			}
			
			private function helptext1Removed(event:Event):void {
				if (Settings.getInstance().getSetting(Settings.SettingsHelpTextEditBGEventViewChangeDateAndTime) == 'true') {
					helpCallOut = new HelpCallOut();
					helpCallOut.helpText = resourceManager.getString('editbgeventview','help_text_changeDateAndTime');
					helpCallOut.settingId = Settings.SettingsHelpTextEditBGEventViewChangeDateAndTime;
					helpCallOut.addEventListener(PickerEvent.PICKER_SET,helptext2Removed);
					helpCallOut.open(timeGroup,true)
				} else {
					helptext2Removed(null);
				}
			}
			
			private function helptext2Removed(event:Event):void {
				
			}
			
			private function meteringResultClicked(e:MouseEvent = null):void {
				amountPicker = new AmountPicker();
				amountPicker.amount = meteringResult.label;
				if (Settings.getInstance().getSetting(Settings.SettingsBLOODGLUCOSE_UNIT) == "mmoll") {
					amountPicker.decimalDigits = 1;
					amountPicker.maxNonDecimalDigits = 2;
				} else {
					amountPicker.decimalDigits = 0;
					amountPicker.maxNonDecimalDigits = 3;
				}
				amountPicker.addEventListener(PickerEvent.PICKER_SET, onAmountSet);
				amountPicker.addEventListener(PickerEvent.PICKER_CANCEL, onAmountCancel);
				amountPicker.labelText1 = meteringText.text;
				PopUpManager.addPopUp(amountPicker, this, true);
				PopUpManager.centerPopUp(amountPicker);
				
				function onAmountCancel(event: PickerEvent): void
				{
					amountPicker.removeEventListener(PickerEvent.PICKER_SET, onAmountSet);
					amountPicker.removeEventListener(PickerEvent.PICKER_CANCEL, onAmountCancel);
				}
				function onAmountSet(event: PickerEvent): void
				{
					amountPicker.removeEventListener(PickerEvent.PICKER_SET, onAmountSet);
					amountPicker.removeEventListener(PickerEvent.PICKER_CANCEL, onAmountCancel);
					meteringResultText = (event.newValue as Number).toString();
				}
				
			}
			
			private function onBackKeyPressed(event:Event):void {
				if (amountPicker != null) {
					if (amountPicker.isPopUp) {
						PopUpManager.removePopUp(amountPicker);
						event.preventDefault();
					}
					amountPicker = null;
				} 
				if (dateOrTimePicker != null) {
					if (dateOrTimePicker.isPopUp) {
						PopUpManager.removePopUp(dateOrTimePicker);
						event.preventDefault();
					}
					dateOrTimePicker = null;
				} 
				if (confirmationPicker != null) {
					if (confirmationPicker.isPopUp) {
						PopUpManager.removePopUp(confirmationPicker);
						event.preventDefault();
					}
				}
			}
			
			/*private function dateClicked(e:MouseEvent = null):void {
				dateOrTimePicker  = new DatePicker();
				(dateOrTimePicker as DatePicker).currentDate = eventDateAndTime;
				dateOrTimePicker.addEventListener(PickerEvent.PICKER_SET, onTimeOrDateSet);
				dateOrTimePicker.addEventListener(PickerEvent.PICKER_CANCEL, onTimeOrDateCancel);
				PopUpManager.addPopUp(dateOrTimePicker, this, true);
				PopUpManager.centerPopUp(dateOrTimePicker);
			}*/
			
			private function onInitialize():void {
				//setting initial values, will be overwritten in oncreationcomplete if data is an medicinevent
				eventDateAndTime = new Date();
				dateText = dateformatter.format(_eventDateAndTime);
				timeText = timeformatter.format(_eventDateAndTime);
				unit =  resourceManager.getString('general',Settings.getInstance().getSetting(Settings.SettingsBLOODGLUCOSE_UNIT));
				meteringTexttext = resourceManager.getString('editbgeventview','value') + ' (' +unit  + ')';
				meteringResultText = 
					(Settings.getInstance().getSetting(Settings.SettingsBLOODGLUCOSE_UNIT) == "mgperdl" ? "100" : "5");
				newBloodGlucoseEvent = true;
				back_label = resourceManager.getString('general','back');
				TrashButton.visible = false;
			}
			
			/*private function onTimeOrDateCancel(event: PickerEvent): void
			{
				dateOrTimePicker.removeEventListener(PickerEvent.PICKER_SET, onTimeOrDateSet);
				dateOrTimePicker.removeEventListener(PickerEvent.PICKER_CANCEL, onTimeOrDateCancel);
			}
			
			private function onTimeOrDateSet(event: PickerEvent): void
			{
				dateOrTimePicker.removeEventListener(PickerEvent.PICKER_SET, onTimeOrDateSet);
				dateOrTimePicker.removeEventListener(PickerEvent.PICKER_CANCEL, onTimeOrDateCancel);
				eventDateAndTime = (event.newValue as Date);
			}
			
			private function timeClicked(e:MouseEvent = null):void {
				dateOrTimePicker = new TimePicker();
				(dateOrTimePicker as TimePicker).currentTime = eventDateAndTime;
				dateOrTimePicker.addEventListener(PickerEvent.PICKER_SET, onTimeOrDateSet);
				dateOrTimePicker.addEventListener(PickerEvent.PICKER_CANCEL, onTimeOrDateCancel);
				PopUpManager.addPopUp(dateOrTimePicker, this, true);
				PopUpManager.centerPopUp(dateOrTimePicker);
			}*/
			
			private function okClicked(e:MouseEvent = null):void {
				MyGATracker.getInstance().trackPageview( "EditBGEventView-OkClicked" );
				
				if (newBloodGlucoseEvent) {
					theBloodGlucoseEvent = new BloodGlucoseEvent(
						new Number(meteringResultText),
						unit,
						DateTimeUtilities.createEventId() + "-glucose",
						comment,//comment
						eventDateAndTime.valueOf(),
						Number.NaN,
						true,
						true);
					ModelLocator.trackingList.addItem(theBloodGlucoseEvent);
				} else {
					theBloodGlucoseEvent.updateBloodGlucoseEvent(unit,new Number(meteringResultText), eventDateAndTime.valueOf(), comment,new Date().valueOf());
				}
				
				ModelLocator.checkYoungestAndOldestDayLine(new Date(theBloodGlucoseEvent.timeStamp));
				if (ModelLocator.trackingList.refresh())
					finishOkClicked();
				else {
					Trace.myTrace("in editbgeventview, setting timeout in finishokclicked");
					setTimeout(finishOkClicked,1000);
				}
			}
			
			private function finishOkClicked(event:Event = null):void {
				//if there's a mealevent after this event within "maximum time difference" for correction factor settings,
				//then trigger a recalculateinsulinamount for that mealevent
				var indexOfThisEvent:Number = ModelLocator.trackingList.getItemIndex(theBloodGlucoseEvent);
				for (var i2:Number = indexOfThisEvent + 1; i2 < ModelLocator.trackingList.length;i2++) {
					var theEventAti2:TrackingViewElement = ModelLocator.trackingList.getItemAt(i2) as TrackingViewElement;
					if (theEventAti2 is MealEvent) {
						(theEventAti2 as MealEvent).recalculateInsulinAmount();
					}
				}
				
				//switch to the tracking view and set trackingeventtoshow to this event
				ModelLocator.trackingEventToShow = theBloodGlucoseEvent.eventid;
				(navigator.parentNavigator as TabbedViewNavigator).selectedIndex = 1;
				((navigator.parentNavigator as TabbedViewNavigator).navigators[1] as ViewNavigator).popToFirstView();
				Synchronize.getInstance().startSynchronize(true,false);
			}
			
			private function deleteEvent(e:MouseEvent = null):void {
				confirmationPicker = new RequestConfirmation();
				confirmationPicker.labelText1 = resourceManager.getString('general','delete_event');
				confirmationPicker.addEventListener(PickerEvent.PICKER_SET, onYesClicked);
				confirmationPicker.addEventListener(PickerEvent.PICKER_CANCEL, onNoClicked);
				
				PopUpManager.addPopUp(confirmationPicker, this, true);
				PopUpManager.centerPopUp(confirmationPicker);
				
				function onYesClicked(event: PickerEvent): void
				{
					MyGATracker.getInstance(this).trackPageview( "EditBGEventView-deleteClicked" );
					
					//if there's a mealevent after this event within "maximum time difference" for correction factor settings,
					//then trigger a recalculateinsulinamount for that mealevent
					//switch to the tracking view and set trackingeventtoshow to this event
					var indexOfThisEvent:Number = ModelLocator.trackingList.getItemIndex(theBloodGlucoseEvent);
					for (var i2:Number = indexOfThisEvent + 1; i2 < ModelLocator.trackingList.length;i2++) {
						var theEventAti2:TrackingViewElement = ModelLocator.trackingList.getItemAt(i2) as TrackingViewElement;
						if (theEventAti2 is MealEvent) {
							(theEventAti2 as MealEvent).recalculateInsulinAmount();
						}
					}

					confirmationPicker.removeEventListener(PickerEvent.PICKER_SET, onYesClicked);
					confirmationPicker.removeEventListener(PickerEvent.PICKER_CANCEL, onNoClicked);
					Synchronize.getInstance().addObjectToBeDeleted(theBloodGlucoseEvent);
					Synchronize.getInstance().startSynchronize(true,false);
					var trackinglistPointer:Number = ModelLocator.trackingList.getItemIndex(theBloodGlucoseEvent);
					theBloodGlucoseEvent.deleteEvent(trackinglistPointer);
					
					ModelLocator.asyncrecalculateInsulinAmoutInAllYoungerMealEvents(theBloodGlucoseEvent.timeStamp, true);					
					//and now make sure the previous item is visible
					if (trackinglistPointer > 0)
						ModelLocator.trackingEventToShow = 
							(ModelLocator.trackingList.getItemAt(trackinglistPointer - 1) as TrackingViewElement).eventid;						
					//switch to the tracking view
					(navigator.parentNavigator as TabbedViewNavigator).selectedIndex = 1;
					((navigator.parentNavigator as TabbedViewNavigator).navigators[1] as ViewNavigator).popToFirstView();
				}
				
				function onNoClicked(event: PickerEvent): void
				{
					confirmationPicker.removeEventListener(PickerEvent.PICKER_SET, onYesClicked);
					confirmationPicker.removeEventListener(PickerEvent.PICKER_CANCEL, onNoClicked);
				}
			}
			
			private function editNote(e:MouseEvent = null):void {
				var dataToPass:Object = new Object();
				dataToPass.title = "";
				dataToPass.text = comment;
				dataToPass.topText = resourceManager.getString('general','editnotetext');
				
				var passbackObject:Object = new Object();
				if (newBloodGlucoseEvent) {
					passbackObject.meteringResultText = meteringResultText;
					passbackObject.unit = unit;
					passbackObject.eventDateAndTime = eventDateAndTime;						
				} else {
					//updating the bg event, this means even if the user would hit the back button afterwards, the bgevent will be updated
					//this is in case user updates eg result or timing, then hits comment button, when coming back, we need to have the new values
					theBloodGlucoseEvent.updateBloodGlucoseEvent(unit,new Number(meteringResultText), eventDateAndTime.valueOf(), comment,new Date().valueOf());
					passbackObject = theBloodGlucoseEvent;
				}
				dataToPass.dataToPassBack = passbackObject;
				navigator.pushView(TextPickerView,dataToPass);				
			}
			
			private function addHandler():void {
				if (!navigator.poppedViewReturnedObject) return; 
				if (!navigator.poppedViewReturnedObject.object) return;
				
				if (navigator.poppedViewReturnedObject.object.dataToPassBack) {
					if (navigator.poppedViewReturnedObject.object.dataToPassBack is BloodGlucoseEvent) {
						theBloodGlucoseEvent = navigator.poppedViewReturnedObject.object.dataToPassBack as BloodGlucoseEvent;
						meteringResultText = (new  Number(theBloodGlucoseEvent.bloodGlucoseLevel)).toString();
						unit = theBloodGlucoseEvent.unit;
						eventDateAndTime = new Date(theBloodGlucoseEvent.timeStamp);
						dateText = dateformatter.format(_eventDateAndTime);
						timeText = timeformatter.format(_eventDateAndTime);
						newBloodGlucoseEvent = false;
					} else
					{
						meteringResultText = navigator.poppedViewReturnedObject.object.dataToPassBack.meteringResultText as String;
						unit = navigator.poppedViewReturnedObject.object.dataToPassBack.unit as String;
						eventDateAndTime = navigator.poppedViewReturnedObject.object.dataToPassBack.eventDateAndTime as Date;
					}
					
					//if there's a datatopassback then there should also be a text
					//anyway let's check
					if (navigator.poppedViewReturnedObject.object.text != null)
						onTextSet(navigator.poppedViewReturnedObject.object.text as String);
				}
			}
			
			private function onTextSet(textSet:String): void
			{
				comment = textSet;
				if (newBloodGlucoseEvent) {
				} else {
					theBloodGlucoseEvent.updateBloodGlucoseEvent(unit,new Number(meteringResultText), eventDateAndTime.valueOf(), comment,new Date().valueOf());
				}
			}
			
			private function dateClicked(e:MouseEvent = null):void {
				tempDate = eventDateAndTime.date;
				tempMonth = eventDateAndTime.month;
				tempYear = eventDateAndTime.fullYear;
				tempHour = eventDateAndTime.hours;
				tempMinute = eventDateAndTime.minutes;
				tempSeconds = eventDateAndTime.seconds;
				
				var dateTime:DateTimeDialogView = Dialog.service.create(
					new DateTimeDialogBuilder()
					.setMode(DialogType.MODE_DATE)
					.setTitle(ModelLocator.resourceManagerInstance.getString("general","date"))
					.setAcceptLabel("Ok")
					.setCancelable(true)
					.setCancelLabel(ModelLocator.resourceManagerInstance.getString('general','cancel'))
					.build()
				);
				dateTime.setDate(tempYear, tempMonth, tempDate);
				dateTime.setTime(tempHour, tempMinute);
				dateTime.addEventListener(DialogViewEvent.CLOSED, dateortime_closedRequestHandler);
				dateTime.addEventListener(DialogDateTimeEvent.SELECTED, date_selectedHandler);
				dateTime.show();
			}
			
			private function timeClicked(e:MouseEvent = null):void {
				tempDate = eventDateAndTime.date;
				tempMonth = eventDateAndTime.month;
				tempYear = eventDateAndTime.fullYear;
				tempHour = eventDateAndTime.hours;
				tempMinute = eventDateAndTime.minutes;
				tempSeconds = eventDateAndTime.seconds;
				
				var dateTime:DateTimeDialogView = Dialog.service.create(
					new DateTimeDialogBuilder()
					.setMode(DialogType.MODE_TIME)
					.setTitle(ModelLocator.resourceManagerInstance.getString("general","time"))
					.setAcceptLabel("Ok")
					.setCancelable(true)
					.setCancelLabel(ModelLocator.resourceManagerInstance.getString('general','cancel'))
					.build()
				);
				dateTime.setDate(tempYear, tempMonth, tempDate);
				dateTime.setTime(tempHour, tempMinute);
				dateTime.addEventListener(DialogViewEvent.CLOSED, dateortime_closedRequestHandler);
				dateTime.addEventListener(DialogDateTimeEvent.SELECTED, time_selectedHandler);
				dateTime.show();
			}
			
			private function time_selectedHandler( event:DialogDateTimeEvent ):void
			{
				tempDate = eventDateAndTime.date;
				tempMonth = eventDateAndTime.month;
				tempYear = eventDateAndTime.fullYear;
				tempHour = event.date.hours;
				tempMinute = event.date.minutes;
				tempSeconds = event.date.seconds;
			}
			
			private function date_selectedHandler( event:DialogDateTimeEvent ):void
			{
				tempDate = event.date.date;
				tempMonth = event.date.month;
				tempYear = event.date.fullYear;
				tempHour = eventDateAndTime.hours;
				tempMinute = eventDateAndTime.minutes;
				tempSeconds = eventDateAndTime.seconds;
			}
			
			private function dateortime_closedRequestHandler( event:DialogViewEvent ):void {
				var newDate:Date = new Date(tempYear, tempMonth, tempDate, tempHour, tempMinute, tempSeconds);
				eventDateAndTime = newDate;
				
			}

		]]>
	</fx:Script>
	<s:navigationContent>
		<s:Button  visible="false" label="{back_label}" id="backButton" click="navigator.popView()" height="48"/>
	</s:navigationContent>
	<s:actionContent>
		<s:Button icon="{ModelLocator.trashImage_48x48}" id="TrashButton" click="deleteEvent(event)"/>
		<s:Button icon="{ModelLocator.notesImage_48x48}" id="NotesButton" click="editNote(event)"/>
		<s:Button icon="{ModelLocator.doneImage_48x48}" id="OkButton" click="okClicked(event)"/>
	</s:actionContent>
	
	<s:Group y="0" x="0" width="100%" height = "100%" left="5" top="5" right="5">
		<s:layout>
			<s:VerticalLayout>
			</s:VerticalLayout>
		</s:layout>
		<s:Label y="0" x="0" width="100%" height="100%" maxHeight="75" text="{resourceManager.getString('editbgeventview','addbgevent')}"  textAlign="center" verticalAlign="middle"/>
		<s:Line width="100%">
			<s:stroke>
				<s:SolidColorStroke color="0xADADAD" weight="1"/>
			</s:stroke>
		</s:Line>	
		<s:Group id="meteringResultGroup" y="0" x="0" width="100%" click="meteringResultClicked(event)">
			<s:layout>
				<s:HorizontalLayout verticalAlign="middle">
				</s:HorizontalLayout>
			</s:layout>
			<s:Label  id="meteringText" height="47" width="60%" text="{meteringTexttext}" verticalAlign="middle"/>
			<s:Button id="meteringResult" height="47" width="40%" label="@{meteringResultText}"  />
		</s:Group>
		<s:Line width="100%">
			<s:stroke>
				<s:SolidColorStroke color="0xADADAD" weight="1"/>
			</s:stroke>
		</s:Line>	
		<s:Group id="datumGroup" y="0" x="0" width="100%" click="dateClicked(event)">
			<s:layout>
				<s:HorizontalLayout verticalAlign="middle">
				</s:HorizontalLayout>
			</s:layout>
			<s:Label  id="datumtext" height="47"  width="30%" text="{resourceManager.getString('general','date')}" verticalAlign="middle"/>
			<s:Button id="datum" height="47"  width="70%" label="@{dateText}" />
		</s:Group>
		<s:Line width="100%">
			<s:stroke>
				<s:SolidColorStroke color="0xADADAD" weight="1"/>
			</s:stroke>
		</s:Line>	
		<s:Group id="timeGroup" y="0" x="0" width="100%" click="timeClicked(event)">
			<s:layout>
				<s:HorizontalLayout verticalAlign="middle">
				</s:HorizontalLayout>
			</s:layout>
			<s:Label  id="timetext" height="47"  width="30%" text="{resourceManager.getString('general','time')}" verticalAlign="middle"/>
			<s:Button id="time" height="47" width="70%" label="@{timeText}" />
		</s:Group>
		<s:Line width="100%">
			<s:stroke>
				<s:SolidColorStroke color="0xADADAD" weight="1"/>
			</s:stroke>
		</s:Line>	
	</s:Group>
</s:View>
